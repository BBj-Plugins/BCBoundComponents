use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::BusinessUIComponents/AbstractBCBoundWidget.bbj::AbstractBCBoundWidget
use ::BusinessUIComponents/BusinessGrid.bbj::BusinessGrid
use ::BusinessUIComponents/bound/TestComponent.bbj::TestComponent
use ::BusinessUIComponents/bound/NavigatorComponent.bbj::NavigatorComponent
use ::BusinessUIComponents/bound/EditToolbarComponent.bbj::EditToolbarComponent
use ::BusinessUIComponents/bound/SimpleFormComponent.bbj::SimpleFormComponent
use ::BusinessUIComponents/bound/FilterComponent.bbj::FilterComponent
use ::BusinessUIComponents/bound/SearchBar.bbj::SearchBar
use ::BusinessUIComponents/bound/MenuBar.bbj::MenuBar
use ::BusinessUIComponents/bound/BCErrorHandler.bbj::BCErrorHandler
use ::BusinessUIComponents/bound/WindowHandler.bbj::WindowHandler
use ::BusinessUIComponents/unbound/HorizontalSplitter.bbj::HorizontalSplitter
use com.basiscomponents.bc.BCBinder
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.ResultSet
use com.basiscomponents.ui.theming.Theme
use java.util.HashMap

class public BusinessMasterDataUI

    field protected BBjTopLevelWindow wnd!
    field protected WindowHandler wh!
    field protected EditToolbarComponent edittb!
    field protected HorizontalSplitter splitter!
    field protected BBjVector forms!
    field protected BBjVector filters!
    field protected BBjTabCtrl formTabCtrl!
    field protected BCBinder binder!
    field protected BBjSysGui sysgui!
    field protected BBjControl topControl!
    field protected BusinessGrid bgrid!
    field protected String title! = ""
    field protected String pgm! = ""
    field private Theme theme! = Theme.getInstance()

    method public BusinessMasterDataUI( BBjString BC$, BBjString TITLE$, BBjString PGMNAME$, ResultSet filter!, ResultSet form_defs!, HashMap controlPrefixMap!)
        ? 'HIDE'
        #sysgui! = BBjAPI().openSysGui("X0")
        #sysgui!.setRepaintEnabled(0)

        if info(3,6)="5" then
            #wnd! = #sysgui!.addWindow(100,100,1020,760,TITLE$,$01001010$)
        else
            #wnd! = #sysgui!.addWindow(100,100,1020,760,TITLE$,$00000093$)
        endif

        #wnd!.setBackColor(#theme!.getBBjBackgroundColor())
        #wnd!.setCallback(BBjAPI.ON_RESIZE, #this!,"resize")
        #splitter! = new HorizontalSplitter(#wnd!,112,2,45,890,800)
        
        #formTabCtrl! = #splitter!.getBottomCanvas().addTabCtrl(115, 0, 0, 500, 500)
        #formTabCtrl!.setBackColor(#theme!.getBBjBackgroundColor())
        #formTabCtrl!.setForeColor(#theme!.getPrimaryColorPalette().getBBjColor("A700"))
        #formTabCtrl!.setFont(#theme!.getBBjFont("h6"))
        
        #splitter!.getBottomCanvas().setBackColor(#theme!.getBBjBackgroundColor())
        #splitter!.getTopCanvas().setBackColor(#theme!.getBBjBackgroundColor())
        
        #forms! = new BBjVector()
        #filters! = new BBjVector()
        #splitter!.setBottomCtrl(#formTabCtrl!)
        
        bc! = eval("new "+BC$+"()")
        #binder! = new BCBinder(bc!)
        #binder!.setDebugLevel(1)
        #title! = TITLE$
        #pgm! = PGMNAME$

        if #pgm!.length() > 40 then
            #pgm! = #pgm!.substring(#pgm!.length() -35)
        endif

        #addMenuBar()
        #addEditToolbar()
        #addBusinessGrid()
        #addBCErrorHandler(TITLE$)
        #addForms(form_defs!, controlPrefixMap!)

        if filter! <> null() then
            #addFilter(filter!, controlPrefixMap!)
        endif

        #addWindowHandler(#pgm!)
        
    methodend

    method public void doModal()
        #binder!.retrieve()
        #delayed_resize(null())
        #sysgui!.setRepaintEnabled(1)
        #wnd!.setVisible(1)
        process_events
    methodend
    
    rem ######### add methods for BC bound components #########
    method public void addForms(ResultSet form_defs!, HashMap controlPrefixMap!)
        cwIdStart = 200
        it! = form_defs!.iterator()
        while it!.hasNext()
            dr! = it!.next()
            formCW! = #splitter!.getBottomCanvas().addChildWindow(cwIdStart + i,0,0,500,500,"",$00000800$, #sysgui!.getAvailableContext(),$$)
            #formTabCtrl!.addTab(dr!.getFieldAsString("TABNAME"), formCW!)

            if (dr!.contains("CLASSNAME")) then
                form! = eval("new "+dr!.getFieldAsString("CLASSNAME")+"(formCW!,100,0,5,300,300)")
            else
                form! = new SimpleFormComponent(formCW!,100,0,5,300,300)
                form!.setForm(dr!.getFieldAsString("ARC"), dr!.getFieldAsNumber("CWID"))
            endif

            if controlPrefixMap! <> null() then
                form!.setControlPrefixMap(controlPrefixMap!)
            endif

            #forms!.addItem(form!)
            #binder!.register(form!)
            cwIdStart = cwIdStart + 1
        wend
    methodend

    method public BCBinder getBinder()
        methodret #binder!
    methodend

    method public void addMenuBar()
        menuBar! = new MenuBar(#wnd!)
        #binder!.register(menuBar!)
        menuBar!.setBinder(#binder!)
        menuBar!.initMasterData()
    methodend

    method public void addEditToolbar()
        #edittb! = new EditToolbarComponent(#wnd!,111,-2,-2,100,32)
        #binder!.register(#edittb!)
    methodend

    method public void addBusinessGrid()
        #bgrid! = new BusinessGrid(#splitter!.getTopCanvas(),160,0,0,960,500)
        #splitter!.setTopCtrl(#bgrid!)
        #binder!.register(#bgrid!)
    methodend

    method public void addBCErrorHandler(String title!)
        errorhandler! = new BCErrorHandler()
        errorhandler!.setTitle(title!)
        #binder!.register(errorhandler!)
    methodend

    method public void addWindowHandler(String pgmName!)
        #wh! = new WindowHandler(#wnd!)
        #wh!.setProgramName(pgmName!)
        #wh!.applyWindowConfig()
        #binder!.register(#wh!)
    methodend

    method public void addFilter(DataRow filter_def!, HashMap controlPrefixMap!)
        if (filter_def!.contains("CLASSNAME")) then
            filter! = eval("new "+filter_def!.getFieldAsString("CLASSNAME")+"(#wnd!,#wnd!.getAvailableControlID(),0,5,300,300)")
        else
            filter! = new FilterComponent(#binder!, #wnd!,#wnd!.getAvailableControlID(),0,25,300,300)
            filter!.setKey(#pgm!)
            filter!.setForm(filter_def!.getFieldAsString("ARC"), filter_def!.getFieldAsNumber("CWID"))
        endif

        if controlPrefixMap! <> null() then
            filter!.setControlPrefixMap(controlPrefixMap!)
        endif

        #topControl! = cast(BBjWidget, filter!)
        REM             #binder!.register(filter!)
        #filters!.addItem(filter!)
        filter!.buildControlMap()
        #splitter!.setLocation(#splitter!.getX(), #splitter!.getY() + #topControl!.getHeight() + #topControl!.getY())
    methodend

    method public void resize(BBjEvent ev!)
        BBjAPI().removeTimer("resize",err=*next)
        BBjAPI().createTimer("resize", .4, #this!, "delayed_resize")
    methodend

    method public void delayed_resize(BBjEvent ev!)
        BBjAPI().removeTimer("resize",err=*next)
        w = #wnd!.getWidth()
        h = #wnd!.getHeight()

        if #topControl! <> null() then
            #topControl!.setSize(w-4, #topControl!.getHeight())
            h = h - #topControl!.getHeight()
        endif

        #splitter!.setSize(w-4 ,h-25)
        for i = 0 to #forms!.size() - 1
            cast(BBjWidget, #forms!.getItem(i)).setSize(w-4, h-25)
        next i

        if #edittb! <> null() then
            #edittb!.redraw(0)
        endif
    methodend

    method public BBjVector getForms()
        methodret #forms!
    methodend

    method public BBjVector getFilters()
        methodret #filters!
    methodend

    method public BBjWindow getWindow()
        methodret #wnd!
    methodend

classend

declare ResultSet form_defs!

rem controlPrefixMap! is optional
controlPrefixMap! = null()
enter form_defs!, filter!, BC$, TITLE$, PGMNAME$, controlPrefixMap!, err=*next
md! = new BusinessMasterDataUI(BC$, TITLE$, PGMNAME$, filter!, form_defs!, controlPrefixMap!)
md!.doModal()
use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::BusinessUIComponents/bound/AbstractBCBoundWidget.bbj::AbstractBCBoundWidget
use ::BusinessUIComponents/BusinessGrid.bbj::BusinessGrid
use ::BusinessUIComponents/bound/TestComponent.bbj::TestComponent
use ::BusinessUIComponents/bound/NavigatorComponent.bbj::NavigatorComponent
use ::BusinessUIComponents/bound/EditToolbarComponent.bbj::EditToolbarComponent
use ::BusinessUIComponents/configurations/ConfigurationButton.bbj::ConfigurationButton
use ::BusinessUIComponents/bound/SimpleFormComponent.bbj::SimpleFormComponent
use ::BusinessUIComponents/bound/SimpleWebFormComponent.bbj::SimpleWebFormComponent
use ::BusinessUIComponents/bound/FilterComponent.bbj::FilterComponent
use ::BusinessUIComponents/bound/SearchBar.bbj::SearchBar
use ::BusinessUIComponents/bound/MenuBar.bbj::MenuBar
use ::BusinessUIComponents/bound/BCErrorHandler.bbj::BCErrorHandler
use ::BusinessUIComponents/bound/WindowHandler.bbj::WindowHandler
use ::BusinessUIComponents/unbound/HorizontalSplitter.bbj::HorizontalSplitter
use com.basiscomponents.bc.BCBinder
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.ResultSet
use java.util.HashMap

class public BusinessMasterDataUI

    field protected BBjTopLevelWindow wnd!
    field protected WindowHandler wh!
    field protected EditToolbarComponent edittb!
    field protected HorizontalSplitter splitter!
    field protected BBjVector forms!
    field protected BBjVector filters!
    field protected BBjTabCtrl formTabCtrl!
    field protected BCBinder binder!
    field protected BBjSysGui sysgui!
    field protected BBjControl topControl!
    field protected BusinessGrid bgrid!
    field protected String title! = ""
    field protected String pgm! = ""
    
    field private BBjChildWindow topWnd!
    field private BBjChildWindow bottomWnd!
    

    method public BusinessMasterDataUI( BBjString BC$, BBjString TITLE$, BBjString PGMNAME$, ResultSet filter!, ResultSet form_defs!, HashMap controlPrefixMap!)
                
        #sysgui! = BBjAPI().openSysGui("X0")
        #wnd! = #sysgui!.addWindow(10,10,800,600,title$,$01101083$)
        #wnd!.setStyle("display","none")

        inj_html! = #wnd!.addHtmlView(998,0,0,0,0,"")
        inj_html!.setNoEdge(1)
        ch=unt
        open (ch)"BusinessUIComponents/css/BusinessUIComponents.css"
        read record (ch,siz=1280000)style$
        close (ch)
        
        style! = style$
        style! = style!.replaceAll("\r","")
        style! = style!.replaceAll("\n","")
        
        script$ = "var styl  = $doc.createElement('style');"
        script$ = script$ + "styl.textContent = """+style!+""";"
        script$ = script$ + "$doc.head.appendChild(styl);"
        
        url$ = "https://fonts.googleapis.com/icon?family=Material+Icons"
        script$ = script$ + "var link = $doc.createElement('link');"
        script$ = script$ + "link.setAttribute('rel','stylesheet');"
        script$ = script$ + "link.setAttribute('href','"+url$+"');"
        script$ = script$ + "$doc.head.appendChild(link);"
                        
        inj_html!.executeScript(script$)
        inj_html!.destroy()        
        
        #wnd!.addStyle("stretch")
        #wnd!.addPanelStyle("stretch")
        #wnd!.setStyle("height","100%")
        #wnd!.setStyle("min-height","100%")
    
        tb! = #wnd!.addChildWindow(100,0,0,1,1,"Row",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        tb!.addPanelStyle("tbar")
        
        tbl! = tb!.addChildWindow(100,0,0,1,1,"Row",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        tbl!.addPanelStyle("tbar")
        tbl!.addStyle("tbarleft")
        
        tbr! = tb!.addChildWindow(101,0,0,1,1,"Row",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        tbr!.addPanelStyle("tbar")
        tbr!.addStyle("tbarright")

        #topWnd! = #wnd!.addChildWindow(101,0,0,10,10,"Row",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #topWnd!.setDockStyle("height","calc( 50% - 25px )")
        #topWnd!.setStyle("height","100%")
        #topWnd!.setStyle("min-height","100%")
        #topWnd!.setPanelStyle("height","100%")
        #topWnd!.setPanelStyle("min-height","100%")
        #topWnd!.setName("topWnd")
    
        #bottomWnd! = #wnd!.addChildWindow(102,0,0,10,10,"Row",$00100800$,BBjAPI().getSysGui().getAvailableContext())
        #bottomWnd!.setDockStyle("height","calc( 50% - 25px )")
        #bottomWnd!.setPanelStyle("height","100%")
        #bottomWnd!.setPanelStyle("min-height","100%")
        
        #bottomWnd!.setName("bottomWnd")
        
REM         #formTabCtrl! = #bottomWnd!.addTabCtrl(700,0,0,10,0) 
REM         #formTabCtrl!.setStyle("width","100%")

       #bottomWnd!.setPanelStyle("padding","10px"   )

        #forms! = new BBjVector()
        #filters! = new BBjVector()
        
        
        bc! = eval("new "+BC$+"()")
        #binder! = new BCBinder(bc!)
        #binder!.setDebugLevel(1)
        #title! = TITLE$
        #pgm! = PGMNAME$

        if #pgm!.length() > 40 then
            #pgm! = #pgm!.substring(#pgm!.length() -35)
        endif

        rem #addMenuBar()
        #addEditToolbar()
        #addBusinessGrid()
        #addBCErrorHandler(TITLE$)
        #addForms(form_defs!, controlPrefixMap!)

        if filter! <> null() then
            #addFilter(filter!, controlPrefixMap!)
        endif

        #addWindowHandler(#pgm!)
        
        #wnd!.setStyle("display","block")

    methodend

    method public void doModal()
        #binder!.retrieve()

        process_events
    methodend
    
    rem ######### add methods for BC bound components #########
    method public void addForms(ResultSet form_defs!, HashMap controlPrefixMap!)
    
            if #formTabCtrl! = null() then
                rem only one form for now!
                dr! = form_defs!.get(0)
    
                    if (dr!.contains("CLASSNAME")) then
                        form! = eval("new "+dr!.getFieldAsString("CLASSNAME")+"(formCW!,100,0,5,300,300)")
                    else
                        form! = new SimpleFormComponent(#bottomWnd!)
                        form!.setDWCForm(dr!.getFieldAsString("DWCFORM"))
                    endif
        
                    if controlPrefixMap! <> null() then
                        form!.setControlPrefixMap(controlPrefixMap!)
                    endif
        
                    #forms!.addItem(form!)
                    #binder!.register(form!)
                                
            else
                cwIdStart = 200
                it! = form_defs!.iterator()
                while it!.hasNext()
                    dr! = it!.next()
                    formCW! = CAST(BBjChildWindow,#bottomWnd!).addChildWindow(cwIdStart + i,0,0,500,500,"",$00100800$, #sysgui!.getAvailableContext(),$$)
                    #formTabCtrl!.addTab(dr!.getFieldAsString("TABNAME"), formCW!)
        
                    if (dr!.contains("CLASSNAME")) then
                        form! = eval("new "+dr!.getFieldAsString("CLASSNAME")+"(formCW!,100,0,5,300,300)")
                    else
                        form! = new SimpleFormComponent(formCW!)
                        form!.setDWCForm(dr!.getFieldAsString("DWCFORM"))
                    endif
                    
                    if controlPrefixMap! <> null() then
                        form!.setControlPrefixMap(controlPrefixMap!)
                    endif
        
                    #forms!.addItem(form!)
                    #binder!.register(form!)
                    cwIdStart = cwIdStart + 1
                wend
            fi
            
       
    methodend

    method public BCBinder getBinder()
        methodret #binder!
    methodend

    method public void addMenuBar()
        menuBar! = new MenuBar(#wnd!)
        #binder!.register(menuBar!)
        menuBar!.setBinder(#binder!)
        menuBar!.initMasterData()
    methodend

    method public void addEditToolbar()
        #edittb! = new EditToolbarComponent(#wnd!.getChildWindow(100).getChildWindow(100))
        #binder!.register(#edittb!)
    methodend

    method public void addBusinessGrid()
        #bgrid! = new BusinessGrid(#topWnd!,#wnd!.getChildWindow(100).getChildWindow(101) )
        if (#splitter! <> null()) then
            #splitter!.setTopCtrl(#bgrid!)
        fi
        #binder!.register(#bgrid!)
    methodend

    method public void addBCErrorHandler(String title!)
        errorhandler! = new BCErrorHandler()
        errorhandler!.setTitle(title!)
        #binder!.register(errorhandler!)
    methodend

    method public void addWindowHandler(String pgmName!)
        #wh! = new WindowHandler(#wnd!)
        #wh!.setProgramName(pgmName!)
        #wh!.applyWindowConfig()
        #binder!.register(#wh!)
    methodend

    method public void addFilter(DataRow filter_def!, HashMap controlPrefixMap!)
        if (filter_def!.contains("CLASSNAME")) then
            filter! = eval("new "+filter_def!.getFieldAsString("CLASSNAME")+"(#wnd!,#wnd!.getAvailableControlID(),0,5,300,300)")
        else
            filter! = new FilterComponent(#binder!, #wnd!,#wnd!.getAvailableControlID(),0,25,300,300)
            filter!.setKey(#pgm!)
            filter!.setForm(filter_def!.getFieldAsString("ARC"), filter_def!.getFieldAsNumber("CWID"))
        endif

        if controlPrefixMap! <> null() then
            filter!.setControlPrefixMap(controlPrefixMap!)
        endif

        #topControl! = cast(BBjWidget, filter!)
        REM             #binder!.register(filter!)
        #filters!.addItem(filter!)
        filter!.buildControlMap()
        #splitter!.setLocation(#splitter!.getX(), #splitter!.getY() + #topControl!.getHeight() + #topControl!.getY())
    methodend


    method public BBjVector getForms()
        methodret #forms!
    methodend

    method public BBjVector getFilters()
        methodret #filters!
    methodend

    method public BBjWindow getWindow()
        methodret #wnd!
    methodend


classend

declare ResultSet form_defs!

rem controlPrefixMap! is optional
controlPrefixMap! = null()
enter form_defs!, filter!, BC$, TITLE$, PGMNAME$, controlPrefixMap!, err=*next

com.basiscomponents.i18n.Props.setLocale(stbl("!LOCALE"))
com.basiscomponents.i18n.Props.setProperyDir(BBjAPI().getFileSystem().resolvePath("BusinessUIComponents/configurations/textProperties/"))
REM ConfigurationButton.setAllowAdmin(Boolean.TRUE)
REM ConfigurationButton.setAllowFactoryDefaults(Boolean.TRUE)

md! = new BusinessMasterDataUI(BC$, TITLE$, PGMNAME$, filter!, form_defs!, controlPrefixMap!)
md!.doModal()
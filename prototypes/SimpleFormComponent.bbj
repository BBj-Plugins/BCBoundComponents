use ::BusinessUIComponents/AbstractBCBoundWidget.bbj::AbstractBCBoundWidget
use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.bc.BCBound
use com.basiscomponents.bc.BCBinder

use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow

rem /**
rem  * A sample BBj Widget Implementation
rem  */
class public SimpleFormComponent extends AbstractBCBoundWidget

    field private BBjWindow cw!
    field private BBjVector fieldlist!

    rem --------------------- constructors -------------------------------------
    
    rem /**
    rem  * disabled default constructor
    rem  */
    method private SimpleFormComponent()
    methodend

    rem /**
    rem  * The constructor that creates the widget on wnd!
    rem * @param BBjWindow wnd!: parent window
    rem * @param BBjInt id: the control ID
    rem * @param BBjInt x: x-location
    rem * @param BBjInt y: y-location
    rem * @param BBjInt w: width
    rem * @param BBjInt h: height
    rem */
    method public SimpleFormComponent(BBjWindow wnd!, BBjInt id!, BBjInt x!, BBjInt y!, BBjInt w!, BBjInt h!)
        #super!.create(wnd!,id!,x!,y!,w!,h!)
    methodend
    

    method public void setForm(BBjString res_name$, BBjNumber id)
        res = BBjAPI().getSysGui().resOpen(res_name$)
        #cw! = #getCanvas().createChildWindow(res, id, 100, 0, 0)
        
    methodend

    rem ---------------------- methods from BBjWidget -------------------------------- 

    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean f_init!)
        if (f_init!) then
        FI
        if (#cw! <> null()) then 
            #cw!.setSize(#getWidth(),#getHeight())
        fi
    methodend

    method private void clearForm()
        if (#fieldlist! = null()) then #buildFieldList()
        
        it! = #fieldlist!.iterator()
        while it!.hasNext()
            n$=it!.next()
            #cw!.getControl(n$).setText("")
        wend
        #cw!.setEnabled(1)
    methodend

    method private void updateScreen()
        if (#cw!= null()) then methodret
        
        if (#fieldlist! = null()) then #buildFieldList()
        
        sel! = #getBinder().getSelection()
        
        if (sel!.size() <> 1) then
            #clearForm()
            methodret
        fi
        rs! = #getBinder().getRS()
        rowkey$ = sel!.get(0)
        rec! = rs!.get(rowkey$)
        it! = #fieldlist!.iterator()
        while it!.hasNext()
            n$=it!.next()
            if rec!.contains(n$) then
                #cw!.getControl(n$).setText(rec!.getFieldAsString(n$))
            else
                #cw!.getControl(n$).setText("")
            endif
        wend
        #cw!.setEnabled(1)
    methodend
    
    method public void onSetData()
        #updateScreen()
    methodend

    method public void onSetSelection()
        #updateScreen()
    methodend

    method private void buildFieldList()
        attributes! = #getBinder().getAttributesRecord()
        l! = attributes!.getFieldNames()
        
        formfields! = #cw!.getAllControls()
        fieldlist! = new BBjVector()
        
        it! = formfields!.iterator()
        
        while it!.hasNext()
            c! = it!.next() 
            n$=c!.getName()
            if l!.contains(n$) then
                fieldlist!.addItem(n$)
                
                #registerCallbacks(c!)
                precision! = attributes!.getFieldPrecision(n$)
                #formatField(c!)
            fi
        wend
        
        #fieldlist! = CAST(BBjVector,fieldlist!)
    methodend
    
    rem /**
    rem  * formats a control by parsing the attributes record and appliying those attributes.
    rem  */
    method private void formatField(BBjControl c!)
        attributes! = #getBinder().getAttributesRecord()
        fieldAttributes! = attributes!.getFieldAttributes(c!.getName())
        precision! = attributes!.getFieldPrecision(c!.getName())
        switch c!.getControlType()
        
            case BBjControl.INPUTE_TYPE
                cast(BBjInputE, c!).setLength(precision!)
                break
                
            case BBjControl.INPUTN_CONTROL
                ?fieldAttributes!
                if fieldAttributes!.containsKey("MASK") then
                    mask! = fieldAttributes!.get("MASK")
                    cast(BBjInputN, c!).setMask(mask!)
                    cast(BBjInputN, c!).setLength(precision!)
                else
                    scale = 0
                    if fieldAttributes!.containsKey("Scale") then
                        scale = num(fieldAttributes!.get("Scale"))
                    endif
                    mask! = "#.#"
                    if scale > 0 then
                        beforeComma = num(precision!) - scale
                        mask! = fill(beforeComma,"#") + cast(BBjInputN, c!).getDotCharacter() + fill(scale, "#")
                        cast(BBjInputN, c!).setLength(precision! + 1)
                    else
                        mask! = fill(num(precision!),"#")
                        cast(BBjInputN, c!).setLength(precision!)
                    endif
                    cast(BBjInputN, c!).setMask(mask!)
                endif
                endif
                if fieldAttributes!.containsKey("Signed") then
                    signed! = fieldAttributes!.get("Signed")
REM                     cast(BBjInputN, c!).setNegateable(signed!)
                    if signed!.equals("1") then
                        cast(BBjInputN, c!).setNegateable(1)
                    else
                        cast(BBjInputN, c!).setNegateable(0)
                    endif
                endif
                cast(BBjInputN, c!).setLength(precision! + 1)
                break
                
            case BBjControl.INPUTD_CONTROL
                if fieldAttributes!.containsKey("MASK") then
                    mask! = fieldAttributes!.get("MASK")
                else
                    mask! = "%Yd-%Mz-%Dz"
                endif
                cast(BBjInputD, c!).setMask(mask!)
                #addCalendarButton(cast(BBjInputD, c!))
                break
            
            case default
                break
        swend
    methodend
    
    method private void addCalendarButton(BBjInputD control!)
        calendarButton! = #getCanvas().addButton(#getCanvas().getAvailableControlID(), control!.getX() + control!.getWidth() - control!.getHeight() + 1, control!.getY() + 1, control!.getHeight() - 2, control!.getHeight() - 2, "")
        calendarButton!.setImageFile("BusinessUIComponents/icon/ic_insert_invitation_black_24dp.png")
        calendarButton!.setImageSize(control!.getHeight() - 2,control!.getHeight() - 2)
        calendarButton!.setUserData(control!)
        calendarButton!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onCalendarButtonPush") 
    methodend
    
    method private void registerCallbacks(BBjControl c!)
        switch c!.getControlType()
            case BBjControl.INPUTE_TYPE
            case BBjControl.INPUTN_TYPE
            case BBjControl.INPUTD_TYPE
                c!.setCallback(BBjAPI.ON_EDIT_MODIFY,#this!,"onControlEditModify") 
                break
            case default
                c!.setCallback(BBjAPI.ON_GAINED_FOCUS,#this!,"onControlGainedFocus") 
                break
        swend
    methodend
    
    method public void setBinder(BCBinder binder!)
        #super!.setBinder(binder!)
    methodend
    
    method public void onControlGainedFocus(BBjGainedFocusEvent ev!)
        #getBinder().sendSignal(BCBinder.SIGNAL_DIRTY)
    methodend
    
    method public void onCalendarButtonPush(BBjButtonPushEvent ev!)
        control! = ev!.getControl()
        inputD! = control!.getUserData()
        cast(BBjInputD, inputD!).calendar()
    methodend
    
    method public void onControlEditModify(BBjEditModifyEvent ev!)
        #getBinder().sendSignal(BCBinder.SIGNAL_DIRTY)
    methodend
    
    rem /**
    rem  * returns the fields of the form with its values as a datarow, if the controlnames correlate to fields in the atributes record
    rem  */
    method public DataRow getFieldsForWrite()
    rem if not enabled: do not return any fields
        if !#cw!.isEnabled() then methodret new DataRow()
        dr! = new DataRow()
        it! = #fieldlist!.iterator()
        rem add all control values where control name = field name in attributes record
        while it!.hasNext()
            field$ = it!.next() 
            control! = #cw!.getControl(field$)
            value! = control!.getText()
            dr!.setFieldValue(field$, value!)
        wend
        methodret dr!
    methodend
    
    method public void onSignal(int signal!, Object payload!)
        binder! = #getBinder()
        switch signal!
            case binder!.SIGNAL_NEW
                #clearForm()
                break
            case default
                break
        swend
    methodend

classend
use ::BusinessUIComponents/AbstractBCBoundWidget.bbj::AbstractBCBoundWidget
use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.bc.BCBound
use com.basiscomponents.bc.BCBinder

use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow

rem /**
rem  * A sample BBj Widget Implementation
rem  */
class public NavigatorComponent extends AbstractBCBoundWidget

    field private BBjNavigator nav!
        
    rem --------------------- constructors -------------------------------------
    
    rem /**
    rem  * disabled default constructor
    rem  */
    method private NavigatorComponent()
    methodend

    rem /**
    rem  * The constructor that creates the widget on wnd!
    rem * @param BBjWindow wnd!: parent window
    rem * @param BBjInt id: the control ID
    rem * @param BBjInt x: x-location
    rem * @param BBjInt y: y-location
    rem * @param BBjInt w: width
    rem * @param BBjInt h: height
    rem */
    method public NavigatorComponent(BBjWindow wnd!, BBjInt id!, BBjInt x!, BBjInt y!, BBjInt w!, BBjInt h!)
        #super!.create(wnd!,id!,x!,y!,w!,h!)
    methodend
    

    rem ---------------------- methods from BBjWidget -------------------------------- 

    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean f_init!)
        if (f_init!) then
            #nav! = #getCanvas().addNavigator(100,0,0,#getWidth(),#getHeight(),"navigate")
            #nav!.setCallback(BBjAPI.ON_NAV_FIRST,#this!,"onNavFirst")
            #nav!.setCallback(BBjAPI.ON_NAV_LAST,#this!,"onNavLast")
            #nav!.setCallback(BBjAPI.ON_NAV_PREVIOUS,#this!,"onNavPrev")
            #nav!.setCallback(BBjAPI.ON_NAV_NEXT,#this!,"onNavNext")
        FI
            
        #nav!.setSize(#getWidth(),#getHeight())

    methodend


    method private void updateScreen()
            sel! = #getBinder().getSelection()
            rs! = #getBinder().getRS()
            #nav!.setText(str(#getBinder().getRS())+ " selected "+str(sel!))
    methodend
    
    method public void onSetData()
        #updateScreen()
    methodend

    method public void onSetSelection()
        #updateScreen()
    methodend

    method public void onNavFirst( BBjNavigatorMoveFirstEvent ev! )
        #getBinder().setSelection(BCBinder.SEL_FIRST)
    methodend
    
    method public void onNavNext ( BBjNavigatorMoveNextEvent ev! )
        #getBinder().setSelection(BCBinder.SEL_NEXT)
    methodend
    
    method public void onNavPrev( BBjNavigatorMovePreviousEvent ev! )
        #getBinder().setSelection(BCBinder.SEL_PREVIOUS)
    methodend
    
    method public void onNavLast( BBjNavigatorMoveLastEvent ev! )
        #getBinder().setSelection(BCBinder.SEL_LAST)
    methodend

classend
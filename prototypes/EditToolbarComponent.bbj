use ::BusinessUIComponents/AbstractBCBoundWidget.bbj::AbstractBCBoundWidget
use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.bc.BCBound
use com.basiscomponents.bc.BCBinder

use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow

rem /**
rem  * A sample BBj Widget Implementation
rem  */
class public EditToolbarComponent extends AbstractBCBoundWidget

    field private BBjToolButton btn_new!
    field private BBjToolButton btn_save!
    field private BBjToolButton btn_delete!
    
    field private Boolean dirty! = BBjAPI.FALSE
        
    rem --------------------- constructors -------------------------------------
    
    rem /**
    rem  * disabled default constructor
    rem  */
    method private EditToolbarComponent()
    methodend

    rem /**
    rem  * The constructor that creates the widget on wnd!
    rem * @param BBjWindow wnd!: parent window
    rem * @param BBjInt id: the control ID
    rem * @param BBjInt x: x-location
    rem * @param BBjInt y: y-location
    rem * @param BBjInt w: width
    rem * @param BBjInt h: height
    rem */
    method public EditToolbarComponent(BBjWindow wnd!, BBjInt id!, BBjInt x!, BBjInt y!, BBjInt w!, BBjInt h!)
        #super!.create(wnd!,id!,x!,y!,w!,h!)
    methodend
    

    rem ---------------------- methods from BBjWidget -------------------------------- 

    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean f_init!)
        if (f_init!) then
            #btn_new! = #getCanvas().addToolButton(100,0,0,20,20,"NEW")
            #btn_new!.setCallback(BBjAPI.ON_TOOL_BUTTON_PUSH,#this!,"onBtnNew")
            #btn_new!.setImageFile("BusinessUIComponents\icon\ic_add_black_24dp.png")
            #btn_new!.setImageSize(20, 20)
            
            #btn_save! = #getCanvas().addToolButton(101,20,0,20,20,"SAVE")
            #btn_save!.setCallback(BBjAPI.ON_TOOL_BUTTON_PUSH,#this!,"onBtnSave")
            #btn_save!.setImageFile("BusinessUIComponents\icon\ic_save_black_24dp.png")
            #btn_save!.setImageSize(20, 20)
            
            #btn_delete! = #getCanvas().addToolButton(102,40,0,20,20,"DELETE")
            #btn_delete!.setCallback(BBjAPI.ON_TOOL_BUTTON_PUSH,#this!,"onBtnDelete")
            #btn_delete!.setImageFile("BusinessUIComponents\icon\ic_close_black_24dp.png")
            #btn_delete!.setImageSize(20, 20)
            
            #enableControls()
        FI
        
    methodend


    method private void enableControls()
        if #getBinder() <> null() then
            #btn_new!.setEnabled(#dirty! = BBjAPI.FALSE)
            #btn_save!.setEnabled(#dirty!)
            #btn_delete!.setEnabled(#getBinder().getSelection().size()>0)
        fi
    methodend
    
    method public void onBtnNew( BBjToolButtonPushEvent ev! )
        #getBinder().sendSignal(BCBinder.SIGNAL_NEW)
    methodend

    method public void onBtnSave( BBjToolButtonPushEvent ev! )
        #getBinder().sendSignal(BCBinder.SIGNAL_SAVE)
        #dirty! = BBjAPI.FALSE
        #enableControls()
    methodend
    
    method public void onBtnDelete( BBjToolButtonPushEvent ev! )
        #getBinder().sendSignal(BCBinder.SIGNAL_DELETE)
    methodend
    
    method public void onSignal(int signal!, Object payload!)
        if signal! = BCBinder.SIGNAL_DIRTY then
            #dirty! = BBjAPI.TRUE
        fi
        #enableControls()
    methodend
    
    method public void onSetSelection()
        #enableControls()
    methodend

    method public void onSetData()
        #enableControls()
    methodend
    
    method public Boolean canSetSelection()
        if #dirty! then
            action = msgbox("Save Data?", 3)
            switch action
                case 6
                    rem action = YES
                    #onBtnSave(null())
                    methodret Boolean.TRUE
                case 7
                    rem action = NO
                    #dirty! = BBjAPI.FALSE
                    #enableControls()
                    methodret Boolean.TRUE
                case 2
                    rem action = CANCEL
                    methodret BBjAPI.FALSE
                case default
                    methodret BBjAPI.FALSE
             swend
        fi
        methodret BBjAPI.TRUE
    methodend
    

classend
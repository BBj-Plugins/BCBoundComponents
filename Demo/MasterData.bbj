use ::BusinessUIComponents/ExampleBCs/ChileCustomerBC.bbj::ChileCustomerBC
use ::BusinessUIComponents/BusinessGrid.bbj::BusinessGrid
use ::BusinessUIComponents/bound/TestComponent.bbj::TestComponent
use ::BusinessUIComponents/bound/NavigatorComponent.bbj::NavigatorComponent
use ::BusinessUIComponents/bound/EditToolbarComponent.bbj::EditToolbarComponent
use ::BusinessUIComponents/bound/SimpleFormComponent.bbj::SimpleFormComponent
use ::BusinessUIComponents/bound/SearchBar.bbj::SearchBar
use ::BusinessUIComponents/bound/BCErrorHandler.bbj::BCErrorHandler
use ::BusinessUIComponents/unbound/HorizontalSplitter.bbj::HorizontalSplitter
use com.basiscomponents.bc.BCBinder
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.ResultSet

BBjAPI().getAdmin("guest", "").getEnvironment().reloadClasspath("uicomponents")

declare ResultSet rs!
rs! = new ResultSet()

declare DataRow dr!
dr! = new DataRow()
dr!.setFieldValue("ARC", "BusinessUIComponents/Demo/MasterDataForm1.arc")
dr!.setFieldValue("CWID", 100)
dr!.setFieldValue("TABNAME", "Personal")
rs!.add(dr!)
dr! = new DataRow()
dr!.setFieldValue("ARC", "BusinessUIComponents/Demo/MasterDataForm2.arc")
dr!.setFieldValue("CWID", 100)
dr!.setFieldValue("TABNAME", "Business")
rs!.add(dr!)

BC$="ChileCustomerBC"

? 'HIDE'
sysgui! = BBjAPI().openSysGui("X0")
sysgui!.setRepaintEnabled(0)
wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,800,600,"BBj Grid Ex Demo")
wnd!        .setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!        .setCallback(BBjAPI.ON_RESIZE,"resize")

nav! = new NavigatorComponent(wnd!,100,85,10,200,20)
edittb! = new EditToolbarComponent(wnd!,101,5,10,60,30)

splitter! = new HorizontalSplitter(wnd!,102,5,30,900,800)

searchBar! = new SearchBar(wnd!,103,510, 10, 285, 20)

bgrid! = new BusinessGrid(splitter!.getTopCanvas(),100,0,0,300,200)
bgrid!.setConfigurationWidgetUser("MyUser")
splitter!.setTopCtrl(bgrid!)

tabCtrl! = splitter!.getBottomCanvas().addTabCtrl(104, 0, 0, 500, 500)

errorHandler! = new BCErrorHandler()

cwIdStart = 200

forms! = new BBjVector()
for i = 0 to rs!.size() - 1
    dr! = rs!.get(i)
    formCW! = splitter!.getBottomCanvas().addChildWindow(cwIdStart + i,0,0,500,500,"",$00000800$,sysgui!.getAvailableContext(),$$)
    tabCtrl!.addTab(dr!.getFieldAsString("TABNAME"), formCW!)
    form! = new SimpleFormComponent(formCW!,100,0,5,300,300)
    form!.setForm(dr!.getFieldAsString("ARC"), dr!.getFieldAsNumber("CWID"))
    forms!.addItem(form!)
next i

splitter!.setBottomCtrl(tabCtrl!)

bc! = eval("new "+BC$+"()")

binder! = new BCBinder(bc!)
binder!.register(nav!)
binder!.register(edittb!)
binder!.register(bgrid!)
binder!.register(searchBar!)
binder!.register(errorHandler!)

for i = 0 to forms!.size() - 1
    binder!.register(forms!.getItem(i))
next i

binder!.retrieve()

splitter!.setSize(wnd!.getWidth() -10,wnd!.getHeight()-30)
nav!.setSize(wnd!.getWidth()-405,20)

for i = 0 to forms!.size() - 1
    forms!.getItem(i).setSize(tabCtrl!.getWidth(), tabCtrl!.getHeight())
next i

sysgui!.setRepaintEnabled(1)
timerActive! = Boolean.FALSE
process_events

resize:
REM   if timerActive! then return
  ev! = BBjAPI().getLastEvent()
  if timerActive! then return
  timerActive! = Boolean.TRUE
  BBjAPI().createTimer("resize",.4,"delayed_resize")
return
delayed_resize:
  timerActive! = Boolean.FALSE
  BBjAPI().removeTimer("resize",err=*next)
  splitter!.setSize(ev!.getWidth() -10 ,ev!.getHeight()-30)
  navWidth = ev!.getWidth()-405
  if navWidth < nav!.getHeight() * 4 then
    navWidth = nav!.getHeight() * 4
  endif
  nav!.setSize(navWidth,20)
  searchBarX = ev!.getWidth()-290
REM   ?searchBarX
REM   if searchBarX < 100 then escape
  if searchBarX < (nav!.getX() +  nav!.getWidth()) then
     searchBarX = nav!.getX() +  nav!.getWidth()
  endif
  searchBar!.setLocation(searchBarX, searchBar!.getY())
return

byebye:
    bye
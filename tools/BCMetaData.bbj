
use com.basiscomponents.bc.BCBinder
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.ResultSet

use com.basiscomponents.i18n.Props

use ::BBjGridExWidget/GxColumns.bbj::GxColumn
use ::BBjGridExWidget/GxColumns.bbj::GxColumnGroup
use ::BBjGridExWidget/GxState.bbj::GxState
use ::BBjGridExWidget/GxRenderers.bbj::GxRendererBoolean
use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use ::BBjGridExWidget/GxCellEditors.bbj::GxCellEditorRichSelect
use ::BBjGridExWidget/GxCellEditors.bbj::GxCellEditorSelect
use ::BBjGridExWidget/GxClientEvents.bbj::GxClientEventsCell
use ::BBjGridExWidget/GxClientModels.bbj::GxClientCellModel

use ::BusinessUIComponents/BusinessGrid.bbj::BusinessGrid
use ::BusinessUIComponents/configurations/ConfigurationButton.bbj::ConfigurationButton
use ::BusinessUIComponents/configurations/CustomConfigurationsBC.bbj::CustomConfigurationsBC
use ::BusinessUIComponents/configurations/IPersistentState.bbj::IPersistentState
use ::BusinessUIComponents/bound/WindowHandler.bbj::WindowHandler

use com.basiscomponents.bc.meta.IBCMetaDataModel

use com.google.gson.JsonArray


use ::DialogWizard/BBDialog.bbj::BBDialog
use ::DialogWizard/DialogUtils.bbj::DialogUtils
use ::DialogWizard/DialogWizardRun.bbj::DialogWizardRun
use ::bbwindowutils.bbj::BBWindowUtils

class public BCMetaData extends BBDialog implements IPersistentState

    field protected BBjAPI        API! = BBjAPI()
    field protected BBjNumber     TRUE = #API!.TRUE
    field protected BBjNumber     FALSE = #API!.FALSE
    field protected BBjSysGui     SysGui! = #API!.openSysGui("X0")
    field protected BBjThinClient ThinClient! = #API!.getThinClient()
    field protected BBjString     ClientLocale$ = #ThinClient!.getClientLocale()
    field protected BBjString     PgmDirectory$ = pgm(-2)(1,max(pos("\"=pgm(-2),-1),pos("/"=pgm(-2),-1)))
    field protected Boolean initialRetrieve! = Boolean.TRUE

    field protected BBjNamespace  GroupNS! = #API!.getGroupNamespace()
    field protected BBjMDI        MDI!
    field protected WindowHandler windowHandler!
    
    field protected String pgmname! = "BCMetaData"
    field protected String state! = ""
    
    field protected String realm! = "GRD"
    
    field protected ConfigurationButton    cfgbutton!
    field protected CustomConfigurationsBC cfgbc!

	rem /** DLGWIZ_RES BCMetaData.arc 101 **/

	rem /** DLGWIZ_FLD_BEGIN **/
	rem /** DLGWIZ_FLD_END **/


	rem /** DLGWIZ_FLC_BEGIN **/
	field protected BBjTopLevelWindow C_BCMetaData!
	field protected BBjStaticText C_BCMetaData__LBL_CLASS!
	field protected BBjListButton C_BCMetaData__LB_CLASS!
	field protected BBjButton C_BTN_SAVE!
	field protected BBjButton C_BTN_GRID!
	rem /** DLGWIZ_FLC_END **/

	field private BBjGridExWidget grid!
	field private DataRow CurrBC!
	field public ResultSet BCList!
	field public BBjString Dir$

	field private BusinessGrid preview_grid!

    field public BBjTopLevelWindow Wnd!
    
    field private IBCMetaDataModel meta_model!
    
    
    
    method private BCMetaData()
    methodend
    
    rem /**
    rem * Constructor BCMetaData
    rem */
    method public BCMetaData(IBCMetaDataModel meta_model!)
        #super!("BusinessUIComponents/tools/BCMetaData.arc",101)
        #meta_model! =  meta_model!


        rem Check if BBjSysGui object is still valid. If not re-create it.
        #SysGui!.getContext(err=*next); sg_ok=1
        if !sg_ok then
            #SysGui!=#API!.openSysGui("X0")
        endif

        if stbl("+USER_LOCALE",err=*endif)<>"" then
            #ClientLocale$ = stbl("+USER_LOCALE")
        endif

        #Wnd!=#super!.getWndTop()
        if #API!.isInMDI() then #MDI!=#API!.getMDI()

        if #Wnd!<>null() then
            #setCallbacks()
        endif
    methodend

    rem /**
    rem * Method setCallbacks:
    rem * Set control callbacks
    rem */
    method protected void setCallbacks()
REM         if #API!.isInMDI() then #MDI!.registerMDIClosingCallback(#this!,"onMDIClosing")
		rem /** DLGWIZ_CBS_BEGIN **/
REM 		#super!.getControlByName("BCMetaData").setCallback(BBjAPI.ON_CLOSE,#this!,"BCMetaData__ON_CLOSE")
    
		#super!.getControlByName("BCMetaData").setCallback(BBjAPI.ON_RESIZE,#this!,"BCMetaData__ON_RESIZE")
		#super!.getControlByName("LB_CLASS").setCallback(BBjAPI.ON_LIST_SELECT,#this!,"LB_CLASS__ON_LIST_SELECT")
		#super!.getControlByName("BTN_SAVE").setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"BTN_SAVE__ON_BUTTON_PUSH")
		#super!.getControlByName("BTN_GRID").setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"BTN_GRID__ON_BUTTON_PUSH")
		rem /** DLGWIZ_CBS_END **/

    methodend
    
    method public void applyWindowHandler()
        #windowHandler! = new WindowHandler(#Wnd!)
        #windowHandler!.setProgramName(#pgmname!)
        #windowHandler!.applyWindowConfig()
    methodend

    rem /**
    rem * Method doDataExchange:
    rem * Set controls with program variables
    rem * @param BBjNumber pInitFlag: Controls are initialized, if true
    rem */
    method protected void doDataExchange(BBjNumber pInitFlag)
		rem /** DLGWIZ_DDX_BEGIN **/
		rem /** DLGWIZ_DDX_END **/

    methodend

    rem /**
    rem * Method doModal:
    rem * Initialize and display dialog (contains PROCESS_EVENTS)
    rem * @see doDataExchange(BBjNumber pInitFlag)
    rem * @see onInitDialog()
    rem * @see onExitDialog()
    rem * @return BBjNumber Answer: 0=Passed required fields check, 1=Dialog canceled/closed
    rem */
    method public BBjNumber doModal()

        #API!.setCustomEventCallback("ReturnAnswer","do_return")
        #API!.setCustomEventCallback("CancelAnswer","do_cancel")

        #doDataExchange(#TRUE)
        #super!.setAllValidated(#TRUE)

        #onInitDialog()
        #Wnd!.setVisible(#TRUE)

        do_process_events:
            process_events,err=*same

        do_return:
            #doDataExchange(#FALSE)
            if #super!.getAllValidated()=0 then
                #super!.setAllValidated(#TRUE)
                goto do_process_events
            endif

        do_cancel:
            #onExitDialog()
            
            #cfgbutton!.saveAutoConfig(#retrieveState())
            #Wnd!.destroy()

        methodret #super!.getAnswer()
    methodend

    rem /**
    rem * Method onInitDialog:
    rem * Your custom initialization
    rem */
    method public void onInitDialog()


    	rem /** DLGWIZ_INI_BEGIN **/
    	#C_BCMetaData! = cast(BBjTopLevelWindow,#super!.getControlByName("BCMetaData"))
    	#C_BCMetaData__LBL_CLASS! = cast(BBjStaticText,#super!.getControlByName("LBL_CLASS"))
    	#C_BCMetaData__LB_CLASS! = cast(BBjListButton,#super!.getControlByName("LB_CLASS"))
    	#C_BTN_SAVE! = cast(BBjButton,#super!.getControlByName("BTN_SAVE"))
    	#C_BTN_GRID! = cast(BBjButton,#super!.getControlByName("BTN_GRID"))
    	rem /** DLGWIZ_INI_END **/
    	
        #C_BTN_SAVE!.setImage(ico!)
        #C_BTN_SAVE!.setImageSize(20,20)

        #C_BTN_GRID!.setImage(ico!)
        #C_BTN_GRID!.setImageSize(20,20)

        
    	v! = new BBjVector()
    	it! = #BCList!.iterator()
    	while it!.hasNext()
    	    v!.addItem(it!.next().getFieldAsString("DESC"))
    	wend
    	#C_BCMetaData__LB_CLASS!.removeAllItems()
    	#C_BCMetaData__LB_CLASS!.insertItems(0,v!)
    	#C_BCMetaData__LB_CLASS!.deselect()
    
    
        #grid! = new BBjGridExWidget(#C_BCMetaData!,500,0,40,#C_BCMetaData!.getWidth(),#C_BCMetaData!.getHeight()-40)
    	grid! = #grid!
        grid!.setTheme( grid!.GRID_THEME_BALHAM() )
        grid!.getOptions().setSingleClickEdit(1)
    
        grid!.setCallback(grid!.ON_GRID_CELL_EDITING_STOPPED(),#this!,"cellEditingStopped")
    
        locales! = #meta_model!.getLocales()
        isEditableExp! = "data.FIELDNAME && (data.FIELDNAME !== '_ENTITY' && data.FIELDNAME !== '_ENTITIES')"

        filedNameColumn! = grid!.addColumn("FIELDNAME")
        filedNameColumn!.setLabel("Field Name")
        filedNameColumn!.setWidth(150)
        filedNameColumn!.setPinned(GxColumn.PINNED_LEFT())
        filedNameColumn!.setEnableFilter(1)

        typeColumn! = grid!.addColumn("TYPE")
        typeColumn!.setLabel("Type")
        typeColumn!.setWidth(80)
        typeColumn!.setPinned(GxColumn.PINNED_LEFT())
        typeColumn!.setEnableFilter(1)

        hideColumn! = grid!.addColumn("HIDE")
        hideColumn!.setLabel("Hide")
        hideColumn!.setWidth(150)
        hideColumn!.setType(java.sql.Types.BOOLEAN)
        hideColumn!.setPinned(GxColumn.PINNED_LEFT())
        hideColumn!.setEditable(1)
        hideColumn!.setEditableExpression(isEditableExp!)
        hideColumn!.setEnableFilter(1)

        alignColumn! = grid!.addColumn("ALIGN")
        alignColumn!.setLabel("Align")
        alignColumn!.setEditable(1)
        alignColumn!.setEditableExpression(isEditableExp!)
        alignColumn!.setWidth(80)
        alignColumn!.setPinned(GxColumn.PINNED_LEFT())
        alignColumn!.setEnableFilter(1)

        rem GxCellEditorRichSelect is only available in the enhanced version
        rem you can fallback to GxCellEditorSelect like this 
        rem  if(!grid!.isLicensed()) then
        rem     edselect! = new GxCellEditorRichSelect()
        rem  else 
        rem     edselect! = new GxCellEditorSelect()
        rem  endif

         edselect! = new GxCellEditorSelect()
         values! = new JsonArray()
         values!.add("")
         values!.add("L")
         values!.add("C")
         values!.add("R")
         edselect!.setValues(values!)
         alignColumn!.setCellEditor(edselect!)

        it1! = locales!.iterator()
        while it1!.hasNext()
            loc$ = it1!.next()
            col! = grid!.addColumn(loc$+"_LABEL","Label")
            col!.setEditable(1)
            col!.setEnableFilter(1)
            col!.getCellEditor().setRequired(0)

            col! = grid!.addColumn(loc$+"_SHORTLABEL","Short")
            col!.setEditable(1)
            col!.setEditableExpression(isEditableExp!)
            col!.setEnableFilter(1)
            col!.getCellEditor().setRequired(0)

            col! = grid!.addColumn(loc$+"_GROUP","Group")
            col!.setEditable(1)
            col!.setEditableExpression(isEditableExp!)
            col!.setEnableFilter(1)
            col!.getCellEditor().setRequired(0)

            col! = grid!.addColumn(loc$+"_SHORTC","Short Cue")
            col!.setEditable(1)
            col!.setEditableExpression(isEditableExp!)
            col!.setEnableFilter(1)
            col!.getCellEditor().setRequired(0)

            col! = grid!.addColumn(loc$+"_LONGC","Long Cue")
            col!.setEditable(1)
            col!.setEditableExpression(isEditableExp!)
            col!.setEnableFilter(1)
            col!.getCellEditor().setRequired(0)

            col! = grid!.addColumn(loc$+"_MASK","Mask")
            col!.setEditable(1)
            col!.setEditableExpression(isEditableExp!)
            col!.setEnableFilter(1)
            col!.getCellEditor().setRequired(0)
        wend


        checkboxRenderer! = new GxRendererBoolean()
        checkboxRenderer!.setTrueValue("<font size=+1>&#9745;</font>")
        checkboxRenderer!.setFalseValue("<font size=+1>&#9744;</font>")
  REM           checkboxRenderer!.setTrueValue("&#10003;")
  REM           checkboxRenderer!.setFalseValue("&#10008;")
        grid!.getColumn("HIDE").setCellRenderer(checkboxRenderer!)

        it1! = locales!.iterator()
        while it1!.hasNext()
            loc$ = it1!.next()
            group! = new BBjVector()
            group!.add(loc$+"_LABEL")
            group!.add(loc$+"_SHORTLABEL")
            group!.add(loc$+"_GROUP")
            group!.add(loc$+"_SHORTC")
            group!.add(loc$+"_LONGC")
            group!.add(loc$+"_MASK")
            grid!.addColumnGroup(loc$ , loc$, group! , 0 , "")
        wend

        rem grid!.updateColumns()
REM         #cfgbc! = new CustomConfigurationsBC(info(3,2))
REM         #cfgbutton! = new ConfigurationButton(#Wnd!, 201, #Wnd!.getWidth()-50, 5, #this!, #realm!, #pgmname!, #cfgbc! )
REM         #cfgbutton!.applyAutoConfig()
    methodend

    rem /**
    rem * Method onExitDialog:
    rem * Your custom exit code
    rem */
    method public void onExitDialog()
    methodend

    rem /**
    rem * Method postCancelAnswer:
    rem * Set dialog answer to "canceled"
    rem */
    method public void postCancelAnswer()
        #super!.setAnswer(0)
        #API!.postCustomEvent("CancelAnswer",null())
    methodend

    rem /**
    rem * Method postReturnAnswer:
    rem * Set dialog answer to "normal return"
    rem */
    method public void postReturnAnswer()
        #super!.setAnswer(1)
        #API!.postCustomEvent("ReturnAnswer",null())
    methodend

    rem /**
    rem * Method onMDIClosing:
    rem * Handler for the MDI closing
    rem */
    method public void onMDIClosing(BBjMDIClosingEvent pEvent!)
        rem uncomment the next line to veto the MDI Close
        rem #MDI!.vetoMDIClose()
        #MDI!.approveMDIClose()
        #postCancelAnswer()
    methodend

	rem /**
	rem  * Method BCMetaData__ON_CLOSE: 
	rem  * Event handler for the ON_CLOSE event for BCMetaData
	rem  * @param BBjCloseEvent
	rem  */
	method public void BCMetaData__ON_CLOSE(BBjCloseEvent pEvent!)
		#postCancelAnswer()
	methodend

	rem /**
	rem  * Method BCMetaData__ON_RESIZE: 
	rem  * Event handler for the ON_RESIZE event for BCMetaData
	rem  * @param BBjResizeEvent
	rem  */
	method public void BCMetaData__ON_RESIZE(BBjResizeEvent ev!)
		  w=ev!.getWidth()
		  h=ev!.getHeight()
REM 		  #cfgbutton!.setLocation(w-40,5)
		  #grid!.setSize(w,h-40)
	methodend

	rem /**
	rem  * Method LB_CLASS__ON_LIST_SELECT: 
	rem  * Event handler for the ON_LIST_SELECT event for LB_CLASS
	rem  * @param BBjListSelectEvent
	rem  */
	method public void LB_CLASS__ON_LIST_SELECT(BBjListSelectEvent pEvent!)
		id = pEvent!.getSelectedIndex()
		#CurrBC! = #BCList!.get(id)
		#loadData()
		bcName$ = str(#CurrBC!)
        while pos("::"=bcName$)>0
            bcName$ = bcName$(pos("::"=bcName$)+2)
        wend
        bcName$ = bcName$ + "_meta"
REM         #cfgbutton!.destroy()
REM         #cfgbutton! = new ConfigurationButton(#Wnd!, 201, 0, 0, #this!, #realm!, bcName$, #cfgbc! )
REM         #cfgbutton!.applyAutoConfig()
	methodend

	method private void loadData()

	       class$  = #CurrBC!.getFieldAsString("CLASS")
	       desc$   = #CurrBC!.getFieldAsString("DESC")
	       bc!     = eval("new "+class$+"()")
	       ar!     = bc!.getAttributesRecord()

	       locales! = #meta_model!.getLocales()

	       meta! = #meta_model!.loadModel(desc$)

          rs! = new ResultSet()

          fields! = ar!.getFieldNames()
          fields!.insertItem(0,"_ENTITIES")
          fields!.insertItem(0,"_ENTITY")

          layout! = meta!.getField("layout",err=*next).getObject()
          if layout! <> null() then
                layout! = layout!.getField("fields").getObject()
          endif


          it! = fields!.iterator()
          while it!.hasNext()
              f$=it!.next()

              f! = new DataRow()
              f!.setFieldValue("FIELDNAME",f$)

              if f$(1,1)="_" then
                f!.setFieldValue("TYPE","CHAR")
                f!.setFieldValue("HIDE","")
                f!.setFieldValue("ALIGN","")
              else
                f!.setFieldValue("TYPE",ar!.getFieldTypeName(f$))
                f!.setFieldValue("HIDE",java.sql.Types.BOOLEAN,Boolean.FALSE)
                f!.setFieldValue("ALIGN","")
              endif

              if layout! <> null() then
                    flayout! = layout!.getField(f$,err=*endif).getObject()
                    if flayout!.contains("ALIGN") then
                        f!.setFieldValue("ALIGN",flayout!.getFieldAsString("ALIGN"))
                    endif
                    if flayout!.contains("HIDE") then
                        f!.setFieldValue("HIDE",str(flayout!.getFieldAsNumber("HIDE")>0))
                    endif
              endif
            
              it1! = locales!.iterator()
              while it1!.hasNext()
                  loc$ = it1!.next()


                  f!.setFieldValue(loc$+"_LABEL","")
                  f!.setFieldValue(loc$+"_SHORTLABEL","")
                  f!.setFieldValue(loc$+"_GROUP","")
                  f!.setFieldValue(loc$+"_SHORTC","")
                  f!.setFieldValue(loc$+"_LONGC","")
                  f!.setFieldValue(loc$+"_MASK","")

                  locmeta! = null()
                  locmeta! = meta!.getField(loc$,err=*next).getObject()
                  if locmeta! <> null() then
                      fmeta! = null()
                      fmeta! = locmeta!.getField("fields").getObject()
                      if fmeta! <> null() then
                          f!.setFieldValue(loc$+"_LABEL",locmeta!.getField("fields",err=*next).getObject().getField(f$,err=*next).getObject().getFieldAsString("LABEL",err=*next))
                          f!.setFieldValue(loc$+"_SHORTLABEL",locmeta!.getField("fields",err=*next).getObject().getField(f$,err=*next).getObject().getFieldAsString("SHORT",err=*next))
                          f!.setFieldValue(loc$+"_GROUP",locmeta!.getField("fields",err=*next).getObject().getField(f$,err=*next).getObject().getFieldAsString("GROUP",err=*next))
                          f!.setFieldValue(loc$+"_SHORTC",locmeta!.getField("fields",err=*next).getObject().getField(f$,err=*next).getObject().getFieldAsString("SHORTCUE",err=*next))
                          f!.setFieldValue(loc$+"_LONGC",locmeta!.getField("fields",err=*next).getObject().getField(f$,err=*next).getObject().getFieldAsString("LONGCUE",err=*next))
                          f!.setFieldValue(loc$+"_MASK",locmeta!.getField("fields",err=*next).getObject().getField(f$,err=*next).getObject().getFieldAsString("MASK",err=*next))
                      endif
                  endif
              wend

              rs!.add(f!)

          wend

        grid! = #grid!
        grid!.updateData(rs!)

	methodend


	rem /**
	rem  * Method BTN_SAVE__ON_BUTTON_PUSH: 
	rem  * Event handler for the ON_BUTTON_PUSH event for BTN_SAVE
	rem  * @param BBjButtonPushEvent
	rem  */
	method public void BTN_SAVE__ON_BUTTON_PUSH(BBjButtonPushEvent pEvent!)
	        dr! = new DataRow()
	        locales! = #meta_model!.getLocales()


	        save! = new DataRow()

	        it!=locales!.iterator()
	        while it!.hasNext()
	            loc$=it!.next()

	            fdr! = new DataRow()
	            fdr!.setFieldValue("fields",new DataRow())

	            save!.setFieldValue(loc$,fdr!)
	        wend
	        
            fdr! = new DataRow()
            fdr!.setFieldValue("fields",new DataRow())
            save!.setFieldValue("layout",fdr!)
	        

	        rs! = #grid!.getRS()
	        it! = rs!.iterator()
	        while it!.hasNext()
	            row! = it!.next()
	            fieldname$=row!.getFieldAsString("FIELDNAME")
	            
	            if fieldname$(1,1)<>"_" then
	            
	            
	                set! = save!.getField("layout").getObject().getField("fields").getObject()
	                align$=row!.getFieldAsString("ALIGN")
	                hide!=row!.getField("HIDE").getBoolean()

	                fdr! = new DataRow()
	                if align$<>"" then
	                    fdr!.setFieldValue("ALIGN",align$)
	                endif
	                if hide! then
                        fdr!.setFieldValue("HIDE",hide!)
                    endif
                    if fdr!.getFieldNames().size()>0 then
                        set!.setFieldValue(fieldname$,fdr!)
                    endif

	            endif
	            
	            
                it1!=locales!.iterator()
                while it1!.hasNext()
                    loc$=it1!.next()

                    loc! = save!.getField(loc$).getObject().getField("fields").getObject()

                    fdr! = new DataRow()

                    value$=""
                    value$=row!.getFieldAsString(loc$+"_LABEL")
                    if value$>"" then
                        fdr!.setFieldValue("LABEL",value$)
                    endif

                    value$=""
                    value$=row!.getFieldAsString(loc$+"_SHORTLABEL")
                    if value$>"" then
                        fdr!.setFieldValue("SHORT",value$)
                    endif

                    value$=""
                    value$=row!.getFieldAsString(loc$+"_GROUP")
                    if value$>"" then
                        fdr!.setFieldValue("GROUP",value$)
                    endif

                    value$=""
                    value$=row!.getFieldAsString(loc$+"_SHORTC")
                    if value$>"" then
                        fdr!.setFieldValue("SHORTCUE",value$)
                    endif

                    value$=""
                    value$=row!.getFieldAsString(loc$+"_LONGC")
                    if value$>"" then
                        fdr!.setFieldValue("LONGCUE",value$)
                    endif

                    value$=""
                    value$=row!.getFieldAsString(loc$+"_MASK")
                    if value$>"" then
                        fdr!.setFieldValue("MASK",value$)
                    endif

                    if fdr!.getFieldNames().size()>0 then
                        loc!.setFieldValue(fieldname$,fdr!)
                    endif
                wend


	        wend


	        #meta_model!.saveModel(save!)

	methodend

	method public void cellEditingStopped(BBjCustomEvent ev!)
	    cellEvent! = ev!.getObject()

        declare auto GxClientEventsCell cellEvent!
        declare auto GxClientCellModel cellModel!
        declare auto DataRow row!

        lastEvent! = BBjAPI().getLastEvent()
        cellEvent! = lastEvent!.getObject()
        cellModel!  = cellEvent!.getCell()

        rem sync the new client row values with the ResultSet
        row! = cellModel!.getRow().updateFromClientRow()


	methodend

	rem /**
	rem  * Method BTN_GRID__ON_BUTTON_PUSH: 
	rem  * Event handler for the ON_BUTTON_PUSH event for BTN_GRID
	rem  * @param BBjButtonPushEvent
	rem  */
	method public void BTN_GRID__ON_BUTTON_PUSH(BBjButtonPushEvent pEvent!)
		if #CurrBC! <> null() THEN

            BC$=#CurrBC!.getFieldAsString("DESC")
            CLASS$=#CurrBC!.getFieldAsString("CLASS")

            bc! = eval("new "+CLASS$+"()")
            binder! = new BCBinder(bc!)
		    wnd! = BBjAPI().getSysGui().addWindow(BBjAPI().getSysGui().getAvailableContext(),10,10,1024,700,"Grid Setup for "+BC$,$00080083$)
		    wnd!.setCallback(BBjAPI.ON_CLOSE,#this!,"destroyGridPreview")
		    wnd!.setCallback(BBjAPI.ON_RESIZE,#this!,"resizeGridPreview")
		    #preview_grid! = new BusinessGrid(wnd!,200,0,0,1024,700)
		    binder!.register(#preview_grid!)
		    ar!= binder!.getAttributesRecord()
		    binder!.retrieve()
		endif
		
	methodend


	method public void destroyGridPreview(BBjCloseEvent ev!)
	    ev!.getWindow().destroy()
	    #preview_grid!=null()
	methodend
	
	method public void resizeGridPreview(BBjResizeEvent ev!)
	    if #preview_grid! <> null() then
	        w = ev!.getWidth()
	        h = ev!.getHeight()
	        #preview_grid!.setSize(w,h)
	    endif
    methodend
    
    rem /**
    rem  * This method will be called to apply the state to the widget.
    rem  *
    rem  * @param state! state string previously stored by the widget
    rem  */
    method public void applyState(String state!)
REM         #grid!.setState(new GxState(state!))
        #state! = state!
        rem on initial retrieve the grid leads longer to load and a longer retrieve is needed for the grid to be ready when the state application happens
        if #initialRetrieve! then
            #initialRetrieve! = Boolean.FALSE
            delay = 2
        else
            delay = .1
        endif
        BBjAPI().createTimer("applyStateDelay", delay, #this!, "applyStateDelay");rem timer because async gridexwidget state must be applied after grid has loaded
    methodend
    
    rem /**
    rem  * This method will be called to apply the state to the widget.
    rem  *
    rem  * @param state! state string previously stored by the widget
    rem  */
    method public void applyStateDelay(BBjEvent ev!)
        BBjAPI().removeTimer("applyStateDelay",err=*next)
        #grid!.setState(new GxState(#state!))
    methodend

    rem /**
    rem  * This method will provide the widget with the current state of the component.<br>
    rem  * The widget will store this data for later retrieval.
    rem  *
    rem  * @return String containing the current state of all columns
    rem  */
    method public String retrieveState()
        methodret #grid!.getState().toString()
    methodend

    rem /**
    rem  * The widget will call this method to register itself for state changes of the component.<br>
    rem  * The method should set a callback that calls the method 'methodName' on 'callable!', whenever the state changes.<br>
    rem  * Event should be a BBjCustomEvent.
    rem  *
    rem  * @param methodName!
    rem  * @param callable!
    rem  */
    method public void setStateChangeCallback(String methodName!, CustomObject callable!)
        #grid!.setCallback(#grid!.ON_GRID_COLUMN_STATE_CHANGE(), callable!, methodName!)
    methodend

classend



REM /**
REM  * ConfigurationStateHandler.bbj
REM  * @author jcorea
REM  * 
REM  * handler class between the configuration widget and the component. registers for callbacks on the component and communicates
REM  * between the widget and the component
REM  *
REM  */

use ::BusinessUIComponents/configurations/IPersistentState.bbj::IPersistentState
use ::BusinessUIComponents/configurations/ConfigurationButton.bbj::ConfigurationButton

class public ConfigurationStateHandler
    field protected IPersistentState statefulObject!
    field protected ConfigurationButton configurationButton!
    field protected Boolean suppressNextStateChangeEvent! = Boolean.FALSE
    
    method public ConfigurationStateHandler(IPersistentState statefulObject!, ConfigurationButton configurationButton!)
        #statefulObject! = statefulObject!
        #configurationButton! = configurationButton!
        configurationButton!.setStateHandler(#this!)
REM         configurationButton!.init()
        #registerCallbacks()
    methodend
    
    rem /**
    rem  * registers callback for statechange on the component for statechange
    rem  */
    method protected void registerCallbacks()
        #statefulObject!.setStateChangeCallback("onStateChange",#this!)
REM         BBjAPI().setCustomEventCallback(#getOnConfigurationChangeEventName(), #this!,"onConfigurationChange")
    methodend
    
    rem /**
    rem  * get called when the state of the component changes.
    rem  * the widget then has to react to the state change
    rem  */
    method public void onStateChange(BBjEvent ev!)
        if #suppressNextStateChangeEvent! then
            #suppressNextStateChangeEvent! = Boolean.FALSE
        else
            #configurationButton!.stateChange()
REM             BBjAPI().postPriorityCustomEvent(#gridconfigurationButton!.getOnStateChangeEventName(), rs!.toJson())
        endif
    methodend
    
    rem /**
    rem  * returns the current state of the component
    rem  */
    method public String getState()
        methodret #statefulObject!.retrieveState()
    methodend
    
    
    rem /**
    rem  * will be called by the widget when the configuration has changed and
    rem  * will apply the state to the object
    rem  */
    method public void configurationChange(String state!)
        #suppressNextStateChangeEvent! = Boolean.TRUE
        #statefulObject!.applyState(state!)
    methodend
    
    rem /**
    rem  * refires the configuration event on the widget which reapplies the current state
    rem  */
    method public void refreshConfiguration()
        #configurationButton!.updateConfiguration()
    methodend

classend
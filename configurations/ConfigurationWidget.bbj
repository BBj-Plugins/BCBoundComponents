REM /**
REM  * GridConfigurationWidget.bbj
REM  * @author jcorea
REM  * Configuration widget to save/load a state of a component in a VKEYED file.
REM  *
REM  */

use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::BBjGridExWidget/BBjGridExWidgetState.bbj::BBjGridExWidgetState
use ::BusinessUIComponents/configurations/IPropertiesManager.bbj::IPropertiesManager
use ::BusinessUIComponents/configurations/PropertiesManager.bbj::PropertiesManager
use ::BusinessUIComponents/configurations/LocalizedPropertiesManager.bbj::LocalizedPropertiesManager
use ::BusinessUIComponents/configurations/CustomConfigurationsBC.bbj::CustomConfigurationsBC
use ::BusinessUIComponents/configurations/ConfigurationStateHandler.bbj::ConfigurationStateHandler
use ::BusinessUIComponents/configurations/IPersistentState.bbj::IPersistentState
use com.basiscomponents.bc.IConfigurationsBC
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.ResultSet
use java.util.UUID
use java.io.File

class public ConfigurationWidget extends BBjWidget

    field private IPropertiesManager uiPropertiesManager!
    field private IPropertiesManager textPropertiesManager!

    field protected BBjWindow container!
    field protected BBjButton buttonDelete!
    field protected BBjButton buttonSave!
    field protected BBjButton buttonAdmin!
    
    field protected Boolean buttonSaveEnabled! = Boolean.FALSE
    field protected Boolean buttonDeleteEnabled! = Boolean.FALSE
    field protected Boolean buttonAdminEnabled! = Boolean.FALSE
    
    field protected Boolean popupMode! = Boolean.FALSE
    
    
    field protected BBjMenuButton popupButton!
    field protected BBjPopupMenu popupMenu!
    field protected BBjMenuItem menuDelete!
    field protected BBjMenuItem menuSave!
    field protected BBjCheckableMenuItem menuAdmin!
    
    
    
    field protected BBjListEdit listEditOptions!
    field protected ResultSet availableConfigs!
    field protected IConfigurationsBC bc!
    field protected Boolean adminModeEnabled!
    field protected Boolean allowAdmin!
    field protected Boolean bcInitialized! = Boolean.FALSE
    
    field protected String realm!
    field protected String keyx!
    field protected String userName!
    
    field protected static BBjColor ADMIN_ENABLED_COLOR! = BBJAPI().makeColor(0,250,0)
    field protected static BBjColor ADMIN_DISABLED_COLOR!  = BBJAPI().makeColor(100,100,100)
    
    field private BBjColor colorFocusGained! = BBjAPI().getSysGui().makeColor(255,255,255)
    field private BBjColor colorFocusLost! = BBjAPI().getSysGui().makeColor(255,255,255)

    field protected static String ON_STATE_CHANGE! = "STATE_CHANGE"
    field protected static String ON_CONFIGURATION_CHANGE! = "CONFIGURATION_CHANGE"
    
REM     field protected String iconButtonDelete! =          "BusinessUIComponents\icon\ic_close_black_24dp.png"
REM     field protected String iconButtonSave! =            "BusinessUIComponents\icon\ic_save_black_24dp.png"
REM     field protected String iconButtonAdminEnabled! =    "BusinessUIComponents\icon\ic_person_black_24dp.png"
REM     field protected String iconButtonAdminDisabled! =   "BusinessUIComponents\icon\ic_person_white_24dp.png"
    
    
    field protected BBjNumber selectedIndex = -1
    field protected String widgetID! = UUID.randomUUID().toString()
    field protected ConfigurationStateHandler stateHandler!
    
    field protected BBjNumber minPaddingLeft = 0
    field protected BBjNumber widgetWidth = 0
    field protected BBjNumber widgetHeight = 0
    field protected BBjNumber listEditPopupHeight = 0
    field protected BBjNumber optionsListeditWidth = 0
    rem /**
    rem  * default constructor disabled
    rem  */
    method private ConfigurationWidget()
    methodend
    
    method public ConfigurationWidget(BBjWindow container!, BBjNumber id, BBjNumber x, BBjNumber y, IPersistentState statePersistable!, Boolean allowAdmin!)
        #this!(container!, id, x, y, statePersistable!, allowAdmin!, "", "", null())
    methodend

    method public ConfigurationWidget(BBjWindow container!, BBjNumber id, BBjNumber x, BBjNumber y, IPersistentState statePersistable!, Boolean allowAdmin!, String realm!, String keyx!, IConfigurationsBC bc!)
        #super!()
        #adminModeEnabled! = Boolean.FALSE
        #allowAdmin! = allowAdmin!
        #realm! = realm!
        #keyx! = keyx!
        #container! = container!
        if bc! <> null() then
            #bc! = bc!
            #userName! = #bc!.getUserName()
        else 
            #userName! = ""
        endif
        #stateHandler! = new ConfigurationStateHandler(statePersistable!, #this!)
        #create(container!,id,x,y,160,20)
    methodend
    
    rem /**
    rem  * sets the bc and the userName from the bc and redraws, which triggers an init
    rem  */
    method public void setBC(IConfigurationsBC bc!)
        #bcInitialized! = Boolean.FALSE
        #bc! = bc!
        #setUserName(#bc!.getUserName())
        #redraw(0)
    methodend
    
    method protected void createPopupButton()
        #popupButton! = #getCanvas().addMenuButton(#getCanvas().getAvailableControlID(), 100,0,20,20, "")
        #popupMenu! = #popupButton!.addDropdownMenu()
REM         #listEditOptions!.setPopupMenu(#popupMenu!)
REM         #popupMenu!.setLocation(#buttonDelete!.getX(), #buttonDelete!.getY()) 
        
REM         #popupMenu! = BBjAPI().getSysGui().addDropdownMenu()
        #menuDelete! = #popupMenu!.insertMenuItem(0, 1000, "Delete")
        #menuDelete!.setCallback(BBjPopupMenu.ON_POPUP_ITEM_SELECT, #this!, "deleteButtonPush")
        #menuSave!   = #popupMenu!.insertMenuItem(1, 1100, "Save")
        #menuSave!.setCallback(BBjPopupMenu.ON_POPUP_ITEM_SELECT, #this!, "saveButtonPush")
        
        if #allowAdmin! then
            #menuAdmin! = #popupMenu!.insertCheckableMenuItem(2, 1200, "Admin",#adminModeEnabled!)
REM             #menuAdmin!.setCallback(#menuAdmin!.ON_MENU_ITEM_SELECT, #this!, "adminButtonPush")
        endif
    methodend
    
    method protected void togglePopupMode(Boolean popupVisible!)
        #popupMode! = popupVisible!
        #updateButtons()
    methodend
    
    method protected void updateButtons()
        if #popupMode! then
            #popupButton!.setVisible(1)
REM             #listEditOptions!.setPopupMenu(#popupMenu!)
REM             #popupMenu!.setVisible(Boolean.TRUE)
            
            #buttonDelete!.setVisible(Boolean.FALSE)
            #buttonSave!.setVisible(Boolean.FALSE)
            if #allowAdmin! then #buttonAdmin!.setVisible(Boolean.FALSE)
        else
            #popupButton!.setVisible(0)
REM             #listEditOptions!.removePopupMenu()
REM             #popupMenu!.setVisible(Boolean.TRUE)
            
            #buttonDelete!.setVisible(#buttonDeleteEnabled!)
            #buttonSave!.setVisible(#buttonSaveEnabled!)
            if #allowAdmin! then #buttonAdmin!.setVisible(Boolean.TRUE)
        endif
    methodend
    
    rem /**
    rem  * sets the username in the widget and also in the bc depending if admin mode is enabled
    rem  */
    method public void setUserName(String userName!)
        #userName! = userName!
        if #bc! = null() then methodret
        if #adminModeEnabled! then
            #bc!.setUserName(#bc!.getAdminUser())
        else
            #bc!.setUserName(userName!)
        endif
    methodend
    
    method public void setRealm(String realm!)
        #realm! = realm!
    methodend
    
    method public void setKeyX(String keyx!)
        #keyx! = keyx!
    methodend
    
    method public String getRealm()
        methodret #realm!
    methodend
    
    method public String getUserName()
        methodret #realm!
    methodend
    
    method public String getKeyX()
        methodret #keyx!
    methodend
    
    
    method public void setMinPaddingLeft(BBjNumber minPaddingLeft)
        #minPaddingLeft = minPaddingLeft
    methodend
    
    method public BBjNumber getMinPaddingLeft()
        methodret #minPaddingLeft
    methodend
    
    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean f_init!)
        if (f_init!) then
            #listEditOptions!   = #getCanvas().addListEdit(#getCanvas().getAvailableControlID(), 0, 1, 100, 20, "")
            #listEditOptions!.setFieldHeight(18)
            #buttonDelete!      = #getCanvas().addButton(#getCanvas().getAvailableControlID(), 100,0,20,20,"")
            #buttonSave!        = #getCanvas().addButton(#getCanvas().getAvailableControlID(), 120,0,20,20,"")
REM             #buttonSave!.setShortCue("save")
            if #allowAdmin! then
                #buttonAdmin!       = #getCanvas().addButton(#getCanvas().getAvailableControlID(), 140,0,20,20,"")
REM                 #buttonAdmin!.setShortCue("Admin mode (currently disabled)")
            endif
            #createPopupButton()
            #toggleSaveButton(Boolean.FALSE)
            #updateProperties()
            #listEditOptions!.setCallback(#listEditOptions!.ON_GAINED_FOCUS, #this!, "controlChangeBackColorOnGainedFocus")
            #listEditOptions!.setCallback(#listEditOptions!.ON_LOST_FOCUS, #this!, "controlChangeBackColorOnLostFocus")
REM             #setCallback(BBjAPI.ON_PAGE_LOADED, #this!, "init")
        FI
        if !#bcInitialized! and #bc!<> null() then
            #init()
REM             BBjAPI().createTimer("configWidgetInit", 3, #this!, "timerInit")
            #bcInitialized! = Boolean.TRUE
        endif
        
REM     field protected BBjNumber widgetWidth = 0
REM     field protected BBjNumber widgetHeight = 0
REM     field protected BBjNumber listEditPopupHeight = 0
        
        xEnd = #getX() + #widgetWidth 
        if xEnd > #container!.getWidth() then xEnd = #container!.getWidth()
        xStart = xEnd - #widgetWidth
        if xStart < #minPaddingLeft then xStart = #minPaddingLeft
        totalLength = xEnd - xStart
        
        
        if totalLength < #optionsListeditWidth then totalLength = #optionsListeditWidth
        buttonsX = #optionsListeditWidth
        buttonsY = 0
        #getCanvas().setLocation(xStart,#getY())
        #listEditOptions!.setLocation(0,1)
        #listEditOptions!.setSize(#optionsListeditWidth, #listEditPopupHeight)
        #listEditOptions!.setFieldHeight(#widgetHeight - 2)
        #buttonDelete!.setLocation(buttonsX,buttonsY)
        #buttonSave!.setLocation(buttonsX + #widgetHeight,buttonsY)
        if #allowAdmin! then
            #buttonAdmin!.setLocation(buttonsX + (2 * #widgetHeight),buttonsY)
        endif
        if totalLength < #widgetWidth then
REM         ?xStart
REM         ?xEnd
REM         escape
            #togglePopupMode(Boolean.TRUE)
        else
            #togglePopupMode(Boolean.FALSE)
        endif
        
    methodend
    
    method public void init(BBjEvent ev!)
        #init()
    methodend
    
    method protected void updateProperties()
        if #getUIPropertiesManager() = null() then #setUIPropertiesManager(PropertiesManager.getInstance())
        if #getTextPropertiesManager() = null() then #setTextPropertiesManager(LocalizedPropertiesManager.getInstance())
        uiProperties! = #getUIPropertiesManager()
        textProperties! = #getTextPropertiesManager()
        #widgetWidth = cast(BBjNumber, uiProperties!.getPropertyAsNumber("CONFIG_WIDGET_WIDTH"))
        #widgetHeight = cast(BBjNumber, uiProperties!.getPropertyAsNumber("CONFIG_WIDGET_HEIGHT"))
        #optionsListeditWidth = #widgetWidth - (3 * #widgetHeight);REM list edit is widget width minus the 3 buttons
        #listEditPopupHeight = cast(BBjNumber, uiProperties!.getPropertyAsNumber("CONFIG_WIDGET_LISTEDIT_POPUP_HEIGHT"))
        #buttonDelete!.setImageFile(uiProperties!.getProperty("CONFIG_WIDGET_BTN_DELETE_IMAGE"))
        size = uiProperties!.getPropertyAsNumber("CONFIG_WIDGET_HEIGHT") - 4
        #buttonDelete!.setImageSize(size,size)
        #buttonDelete!.setShortCue(textProperties!.getProperty("CONFIG_WIDGET_BTN_DELETE_SHORT_CUE"))
        #buttonSave!.setImageFile(uiProperties!.getProperty("CONFIG_WIDGET_BTN_SAVE_IMAGE"))
        #buttonSave!.setImageSize(size,size)
        #buttonSave!.setShortCue(textProperties!.getProperty("CONFIG_WIDGET_BTN_SAVE_SHORT_CUE"))
        #popupButton!.setLocation(#optionsListeditWidth, 0)
        if #allowAdmin! then
            #buttonAdmin!.setImageSize(size,size)
            if #adminModeEnabled! then
                #buttonAdmin!.setImageFile( uiProperties!.getProperty("CONFIG_WIDGET_BTN_ADMIN_ENABLED_IMAGE"))
                #buttonAdmin!.setShortCue( textProperties!.getProperty("CONFIG_WIDGET_BTN_ADMIN_ENABLED_SHORT_CUE"))
            else
                #buttonAdmin!.setImageFile( uiProperties!.getProperty("CONFIG_WIDGET_BTN_ADMIN_DISABLED_IMAGE"))
                #buttonAdmin!.setShortCue( textProperties!.getProperty("CONFIG_WIDGET_BTN_ADMIN_DISABLED_SHORT_CUE"))
            endif
        endif
        r = uiProperties!.getPropertyAsNumber("CONTROL_GAINED_FOCUS_RED")
        g = uiProperties!.getPropertyAsNumber("CONTROL_GAINED_FOCUS_GREEN")
        b = uiProperties!.getPropertyAsNumber("CONTROL_GAINED_FOCUS_BLUE")
        #colorFocusGained! = BBjAPI().getSysGui().makeColor(r,g,b,err=*next)
        r = uiProperties!.getPropertyAsNumber("CONTROL_LOST_FOCUS_RED")
        g = uiProperties!.getPropertyAsNumber("CONTROL_LOST_FOCUS_GREEN")
        b = uiProperties!.getPropertyAsNumber("CONTROL_LOST_FOCUS_BLUE")
        #colorFocusLost! = BBjAPI().getSysGui().makeColor(r,g,b,err=*next)
    methodend
    
    rem /**
    rem  * sets the ConfigurationStateHandler to handle communication between the widget and the stateful component
    rem  */
    method public void setStateHandler(ConfigurationStateHandler stateHandler!)
        #stateHandler! = stateHandler!
    methodend
    
    method public ConfigurationStateHandler getStateHandler()
        methodret #stateHandler!
    methodend
    
    rem /**
    rem  * initialisation method. will be called by the state handler. sets icons, callbacks and loads/applies the available configurations
    rem  */
    method public void init()
REM         #setIcons()
        #fetchAndFillOptionListEdit()
        #setCallbacks()
        rem first try to apply auto config, that should be the last opened config which would be saved on program exit
        autoConfig! = #getAutoConfig()
        if autoConfig! <> null() then
            config! = autoConfig!.getFieldAsString(#bc!.getFieldNameConfig())
            #stateHandler!.configurationChange(config!)
            methodret
        endif
        rem if no autoconfig, apply the default config and select it in the list edit
        if !#availableConfigs!.isEmpty() then 
            #selectedIndex = 0
            effectiveConfigs! = #bc!.getEffectiveConfiguration()
            if !effectiveConfigs!.size() then methodret
            effectiveConfig! = effectiveConfigs!.get(0)
            #availableConfigs!.iterator()
            if #availableConfigs!.size() then
                for i=0 to #availableConfigs!.size() - 1
                    availableConfig! = #availableConfigs!.get(i)
                    if availableConfig!.equals(effectiveConfig!) then
                        #selectedIndex = i
                        break
                    endif
                next i
            endif
            #listEditOptions!.selectIndex(#selectedIndex)
            #fireConfigurationChangeEvent()
        endif
    methodend
    
    method protected DataRow getAutoConfig()
        oldFilter! = #bc!.getFilter()
        autoFilter! = oldFilter!.clone()
        key! = #keyx! + "_auto"
        autoFilter!.setFieldValue(#bc!.getFieldNameKeyx(), key!)
        autoFilter!.setFieldValue(#bc!.getFieldNameSetting(), "")
        #bc!.setFilter(autoFilter!)
REM         escape
        rs! = #bc!.retrieve()
        #bc!.setFilter(oldFilter!)
        if rs!.size() then
            methodret rs!.get(0)
        else
            methodret null()
        endif
REM         #stateHandler!.configurationChange(config!)
    methodend
    
    method public void saveAutoConfig(String config!)
        selectedConfig! = #getDataRowForWrite()
        key! = #keyx! + "_auto"
        selectedConfig!.setFieldValue(#bc!.getFieldNameKeyx(), key!)
        selectedConfig!.setFieldValue(#bc!.getFieldNameSetting(), "")
        selectedConfig!.setFieldValue(#bc!.getFieldNameConfig(), config!)
        oldFilter! = #bc!.getFilter()
        #bc!.setFilter(selectedConfig!)
        #bc!.write(selectedConfig!)
        #bc!.setFilter(oldFilter!)
    methodend
    
    
    method public String getOnStateChangeEventName()
        methodret #widgetID! + #ON_STATE_CHANGE!
    methodend
    
    method public String getOnConfigurationChangeEventName()
        methodret #widgetID! + #ON_CONFIGURATION_CHANGE!
    methodend
    
    rem /**
    rem  * fetches the available configs from the bc and updates the options list-edit
    rem  */
    method protected void fetchAndFillOptionListEdit()
        dr! = new DataRow()
        dr!.setFieldValue(#bc!.getFieldNameRealm(), #realm!)
        dr!.setFieldValue(#bc!.getFieldNameKeyx(), #keyx!)
        dr!.setFieldValue(#bc!.getFieldNameUserid(), #userName!)
        #bc!.setFilter(dr!)
        #availableConfigs! = #bc!.getAvailableConfigurations()
        #fillOptionListEdit()
    methodend
    
    rem /**
    rem  * only updates the options list-edit without fetching new configurations from the bc
    rem  */
    method protected void fillOptionListEdit()
        #listEditOptions!.removeAllItems()
        if #availableConfigs! = null() OR #availableConfigs!.isEmpty() then
            methodret
        endif
        for i=0 to #availableConfigs!.size() - 1
            currentConfig! = #availableConfigs!.get(i)
            userID! = currentConfig!.getFieldAsString(#bc!.getFieldNameUserid())
            userSettingName! = currentConfig!.getFieldAsString(#bc!.getFieldNameSetting())
            description! = ""
            if !userID!.trim().equals("") then
                description! = "(" + userID!.trim() + ")"
            else
                description! = "(" + #getTextPropertiesManager().getProperty("CONFIG_WIDGET_DEFAULT_CONFIG_DESCRIPTION") + ")"
            endif
            description! = description! + " " + userSettingName!.trim()
            #listEditOptions!.addItem(description!)
        next i
REM         #listEditOptions!.addItem("")
    methodend
    
    rem /**
    rem  * returns the configuration as datarow which is currently selected in the list edit
    rem  */
    method protected DataRow getSelectedConfig()
        if #availableConfigs! = null() or #availableConfigs!.isEmpty() or #selectedIndex < 0 then
            methodret null()
        endif
        if #selectedIndex >= #availableConfigs!.size() then methodret null()
        methodret #availableConfigs!.get(#selectedIndex)
    methodend
    
    rem /**
    rem  * sets callbacks for the grid, buttons and listedit
    rem  */
    method protected void setCallbacks()
        #listEditOptions!.setCallback(BBjAPI.ON_EDIT_MODIFY, #this!, "listEditOptionsModify")
        #listEditOptions!.setCallback(BBjAPI.ON_LIST_SELECT, #this!,    "configsListEditSelect")
        #buttonDelete!.setCallback(BBjAPI.ON_BUTTON_PUSH, #this!,   "deleteButtonPush")
        #buttonSave!.setCallback(  BBjAPI.ON_BUTTON_PUSH, #this!, "saveButtonPush")
        if #allowAdmin! then
            #buttonAdmin!.setCallback( BBjAPI.ON_BUTTON_PUSH, #this!, "adminButtonPush")
        endif
REM         BBjAPI().setCustomEventCallback(#getOnStateChangeEventName(), #this!,"onColumnStateChange")
    methodend
    
    rem /**
    rem  * Control validation for the list edit. 
    rem  * Makes sure, setting name is saved after user typed it in the list edit
    rem  * Only when admin mode is enabled, user should be able to save default settings
    rem  */
    method public void listEditOptionsModify(BBjEvent ev!)
        #toggleSaveButton(Boolean.TRUE)
    methodend
    
    rem /**
    rem  * in the listEdit, selects the index related to the given data row. only key fields are being considered.
    rem  */
    method protected void selectDataRowInOptions(DataRow dr!)
        if dr! = null() or dr!.isEmpty() or #availableConfigs! = null() or #availableConfigs!.isEmpty() then
            goto deselect
        endif
        seterr deselect
        userid!     = dr!.getFieldAsString(#bc!.getFieldNameUserid()).trim()
        realm!      = dr!.getFieldAsString(#bc!.getFieldNameRealm()).trim()
        keyx!       = dr!.getFieldAsString(#bc!.getFieldNameKeyx()).trim()
        setting!    = dr!.getFieldAsString(#bc!.getFieldNameSetting()).trim()
        for i=0 to #availableConfigs!.size() - 1
            seterr nexti
            currentDR! = #availableConfigs!.get(i)
            currentUserid!     = currentDR!.getFieldAsString(#bc!.getFieldNameUserid()).trim()
            currentRealm!      = currentDR!.getFieldAsString(#bc!.getFieldNameRealm()).trim()
            currentKeyx!       = currentDR!.getFieldAsString(#bc!.getFieldNameKeyx()).trim()
            currentSetting!    = currentDR!.getFieldAsString(#bc!.getFieldNameSetting()).trim()
            if userid!.equals(currentUserid!) and realm!.equals(currentRealm!) and keyx!.equals(currentKeyx!) and setting!.equals(currentSetting!) then
                #selectedIndex = i
                #listEditOptions!.selectIndex(#selectedIndex)
                #fireConfigurationChangeEvent()
                methodret
            endif
            nexti:
        next i
        #fireConfigurationChangeEvent()
        methodret
        deselect:
            #selectedIndex = -1
            #listEditOptions!.deselect()
            #fireConfigurationChangeEvent()
    methodend
    
    rem /**
    rem  * @return returns whether the given DataRow would overwrite an existing config when using this datarow in bc.write().
    rem  * when false, this datarow would create a new entry. 
    rem  * @param dr! datarow which is to be checked
    rem  */
    method protected Boolean wouldWriteOverrideOtherConfig(DataRow dr!)
        dr! = dr!.clone()
        if !#adminModeEnabled! then
            dr!.setFieldValue(#bc!.getFieldNameUserid(), #userName!)
        endif
        previousFilter! = new DataRow()
        previousFilter! = #bc!.getFilter(err=*next)
        #bc!.setFilter(dr!)
        rs! = #bc!.retrieve()
        #bc!.setFilter(previousFilter!)
        if rs!.size() > 0 then methodret Boolean.TRUE
        methodret Boolean.FALSE
    methodend
    
    rem /**
    rem  * returns the text of the list edit without the preceding user tag "(default)" or "(user)"
    rem  */
    method protected String getListEditOptionText()
        text! = #listEditOptions!.getEditText()
        if text! = null() or text!.trim().equals("") then methodret ""
        adminPrefix! = "(default)"
        userPrefix! = "(" + #userName!+ ")"
        if text!.startsWith(adminPrefix!) then
            methodret text!.substring(adminPrefix!.length()).trim()
        endif
        if text!.startsWith(userPrefix!) then
            methodret text!.substring(userPrefix!.length()).trim()
        endif
        methodret text!.trim()
    methodend
    
    rem /**
    rem  * event handler for delete button push
    rem  * deletes the currently selected configuration and selects the successor (or predecessor if last).
    rem  */
    method public void deleteButtonPush(BBjEvent ev!)
        seterr removalerror
        toBeDeletedDR! = #getSelectedConfig()
        if toBeDeletedDR! = null() or toBeDeletedDR!.isEmpty() then methodret
        #bc!.remove(toBeDeletedDR!)
        #fetchAndFillOptionListEdit()
        if #availableConfigs! = null() or #availableConfigs!.size() < 1 then
            #selectedIndex = -1
            #listEditOptions!.deselect()
        else
            if #selectedIndex >= #availableConfigs!.size() then
                #selectedIndex = #availableConfigs!.size() - 1
            endif
            #listEditOptions!.selectIndex(#selectedIndex)
            #fireConfigurationChangeEvent()
        endif
        #updateButtonDelete()
        methodret
        removalerror:
            exc! = BBjAPI().getLastJavaException()
            if exc! <> null() then
                a = msgbox(exc!.getMessage())
            endif
    methodend
    
    rem /**
    rem  * onclick event for the admin button. toggles mode and image
    rem  */
    method public void adminButtonPush(BBjEvent ev!)
        if #adminModeEnabled! then
            #adminModeEnabled! = Boolean.FALSE
            #buttonAdmin!.setImageFile( #getUIPropertiesManager().getProperty("CONFIG_WIDGET_BTN_ADMIN_DISABLED_IMAGE"))
            #buttonAdmin!.setShortCue( #getTextPropertiesManager().getProperty("CONFIG_WIDGET_BTN_ADMIN_DISABLED_SHORT_CUE"))
            #menuAdmin!.setSelected(Boolean.FALSE)
            #bc!.setUserName(#userName!)
        else
            #adminModeEnabled! = Boolean.TRUE
            #buttonAdmin!.setImageFile( #getUIPropertiesManager().getProperty("CONFIG_WIDGET_BTN_ADMIN_ENABLED_IMAGE"))
            #buttonAdmin!.setShortCue( #getUIPropertiesManager().getProperty("CONFIG_WIDGET_BTN_ADMIN_ENABLED_SHORT_CUE"))
            #menuAdmin!.setSelected(Boolean.TRUE)
            #bc!.setUserName(#bc!.getAdminUser())
        endif
        #updateButtonDelete()
    methodend
    
    rem /**
    rem  * event handler for save button push
    rem  */
    method public void saveButtonPush(BBjEvent ev!)
        #saveConfiguration()
    methodend
    
    rem /**
    rem  * event handler for config list edit selection
    rem  * disables the save button, fires change event and shows/unshows delete button
    rem  * if selected row did not change, do nothing
    rem  */
    method public void configsListEditSelect(BBjListSelectEvent ev!)
        if ev!.getSelectedIndex() = #selectedIndex then methodret
        #selectedIndex = ev!.getSelectedIndex()
        selectedConf! = #getSelectedConfig()
        #toggleSaveButton(Boolean.FALSE)
        #fireConfigurationChangeEvent()
        #updateButtonDelete()
    methodend
    
    rem /**
    rem  * if a configuration is selected and the user has permission to delete this configuration, enables the delete button.
    rem  * otherwise the deletebutton is not enabled (not visible)
    rem  */
    method protected void updateButtonDelete()
        selectedConf! = #getSelectedConfig()
        if selectedConf! = null() or selectedConf!.isEmpty() then
            #toggleDeleteButton(Boolean.FALSE)
            methodret
        endif
        #toggleDeleteButton(#bc!.canModify(selectedConf!))
    methodend
    
    rem /**
    rem  * defines what should happen when the state of the component changes.
    rem  * right now, the save button will enable
    rem  */
    method public void stateChange()
        #toggleSaveButton(Boolean.TRUE)
    methodend
    
    method public void toggleMenuSaveButton(Boolean visible!)
        if visible! and #menuSave! <> null() then methodret
        if !visible! and #menuSave! = null() then methodret
        #popupMenu!.removeMenuItem(#menuSave!,err=*next)
        #menuSave! = null()
        if visible! then
            #menuSave! = #popupMenu!.insertMenuItem(1, 1100, "Save")
            #menuSave!.setCallback(BBjPopupMenu.ON_POPUP_ITEM_SELECT, #this!, "saveButtonPush")
        endif
    methodend
    
    
    method public void toggleMenuDeleteButton(Boolean visible!)
        if visible! and #menuDelete! <> null() then methodret
        if !visible! and #menuDelete! = null() then methodret
        #popupMenu!.removeMenuItem(#menuDelete!,err=*next)
        #menuDelete! = null()
        if visible! then
            #menuDelete! = #popupMenu!.insertMenuItem(0, 1100, "Delete")
            #menuDelete!.setCallback(BBjPopupMenu.ON_POPUP_ITEM_SELECT, #this!, "deleteButtonPush")
        endif
    methodend
    
    method public void toggleSaveButton(Boolean visible!)
        if visible! then
            #buttonSaveEnabled! = Boolean.TRUE
        else
            #buttonSaveEnabled! = Boolean.FALSE
        endif
        #toggleMenuSaveButton(visible!)
        #updateButtons()
    methodend
    
    method public void toggleDeleteButton(Boolean visible!)
        if visible! then
            #buttonDeleteEnabled! = Boolean.TRUE
        else
            #buttonDeleteEnabled! = Boolean.FALSE
        endif
        #toggleMenuDeleteButton(visible!)
        #updateButtons()
    methodend
    
    
    rem /**
    rem  * notifies the state handler that a configuration has changed. the statehandler will then apply this new config to the component.
    rem  */
    method public void fireConfigurationChangeEvent()
        entry! = #getSelectedConfig()
        if entry! = null() then methodret
        config! = entry!.getFieldAsString(#bc!.getFieldNameConfig(), err=*next)
        if config! = null() or config!.trim().equals("") then methodret
        #stateHandler!.configurationChange(config!)
    methodend
    
    rem /**
    rem  * saves the configuration and refetches the configurations in the listEdit. also disables the save button.
    rem  * 
    rem  */
    method public void saveConfiguration()
REM         if #currentConfig!.equals("") then methodret
REM         gridstate! = #grid!.getColumnState()
        selectedConfig! = #getDataRowForWrite()
        #bc!.write(selectedConfig!)
        #fetchAndFillOptionListEdit()
        #selectDataRowInOptions(selectedConfig!)
REM         #currentConfig! = ""
        #toggleSaveButton(Boolean.FALSE)
        #updateButtonDelete()
    methodend
    
    rem /**
    rem  * returns a new dataRow with the right keys depending on user listedit input and admin mode
    rem  */
    method protected DataRow getDataRowForWrite()
        selectedConfig! = #getSelectedConfig()
        if selectedConfig! = null() then 
            selectedConfig! = new DataRow()
            selectedConfig!.setFieldValue(#bc!.getFieldNameKeyx(), #keyx!)
            selectedConfig!.setFieldValue(#bc!.getFieldNameRealm(), #realm!)
        endif
        selectedConfig!.setFieldValue(#bc!.getFieldNameSetting(), #getListEditOptionText())
        selectedConfig!.setFieldValue(#bc!.getFieldNameConfig(), #stateHandler!.getState())
        if #adminModeEnabled! then
            selectedConfig!.setFieldValue(#bc!.getFieldNameUserid(), #bc!.getAdminUser())
        else
            selectedConfig!.setFieldValue(#bc!.getFieldNameUserid(), #userName!)
        endif
        methodret selectedConfig!
    methodend
    
    method public void controlChangeBackColorOnGainedFocus(BBjGainedFocusEvent ev!)
        control! = ev!.getControl()
        control!.setBackColor(#colorFocusGained!)
    methodend
    
    method public void controlChangeBackColorOnLostFocus(BBjLostFocusEvent ev!)
        control! = ev!.getControl()
        control!.setBackColor(#colorFocusLost!)
    methodend
    

     rem ------------------properties manager--------------------------------------
    method public void setUIPropertiesManager(IPropertiesManager propertiesManager!)
        #uiPropertiesManager! = propertiesManager!
        #updateProperties()
    methodend
    
    method public IPropertiesManager getUIPropertiesManager()
        methodret #uiPropertiesManager!
    methodend
    
    method public void setTextPropertiesManager(IPropertiesManager propertiesManager!)
        #textPropertiesManager! = propertiesManager!
        #updateProperties()
    methodend
    
    method public IPropertiesManager getTextPropertiesManager()
        methodret #textPropertiesManager!
    methodend

classend
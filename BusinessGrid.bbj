
use com.basiscomponents.bc.BCBinder
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.util.SqlTypeNames

use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget

use ::BBjGridExWidget/GxColumns.bbj::GxColumn
use ::BBjGridExWidget/GxContextMenu.bbj::GxContextMenu
use ::BBjGridExWidget/GxContextMenu.bbj::GxContextMenuItem
use ::BBjGridExWidget/GxRenderers.bbj::GxRendererCustomHTML
use ::BBjGridExWidget/GxRenderers.bbj::GxRendererBoolean
use ::BBjGridExWidget/GxState.bbj::GxState

use ::BusinessUIComponents/AbstractBCBoundWidget.bbj::AbstractBCBoundWidget
use ::BusinessUIComponents/ExportMenuButton.bbj::ExportMenuButton

use ::BusinessUIComponents/configurations/ConfigurationWidget.bbj::ConfigurationWidget
use ::BusinessUIComponents/configurations/CustomConfigurationsBC.bbj::CustomConfigurationsBC
use ::BusinessUIComponents/configurations/IPersistentState.bbj::IPersistentState

use java.util.HashMap

rem /**
rem  * BBj Business Grid<br>
rem  * Uses a Business Component (BC), BCBinder and BBjGridExWidget.
rem  */
class public BusinessGrid extends AbstractBCBoundWidget implements IPersistentState

    field private BBjGridExWidget grid!
    field private Boolean sending! = BBjAPI.FALSE
    field private Boolean ignoreNext! = BBjAPI.FALSE

    field private ConfigurationWidget    cfgwidget!
    field private CustomConfigurationsBC cfgbc!
    field private ExportMenuButton       exportButton!
    field private BBjMenuButton          fitToGridButton!

    field private Boolean columnConfigurationSet! = Boolean.FALSE

    rem --------------------- constructors -------------------------------------

    rem /**
    rem  * disabled default constructor
    rem  */
    method private BusinessGrid()
    methodend

    rem /**
    rem  * The constructor that creates the child-window (canvas) on which the widget is created.
    rem  *
    rem  * @param BBjWindow wnd!: parent window
    rem  * @param BBjInt id: the control ID
    rem  * @param BBjInt x: x-location
    rem  * @param BBjInt y: y-location
    rem  * @param BBjInt w: width
    rem  * @param BBjInt h: height
    rem  */
    method public BusinessGrid(BBjWindow wnd!, BBjInt id!, BBjInt x!, BBjInt y!, BBjInt w!, BBjInt h!)
        #super!.create(wnd!,id!,x!,y!,w!,h!)
    methodend

    rem /**
    rem  * The constructor that an existing child-window (canvas) on which the widget is created.
    rem  */
    method public BusinessGrid(BBjChildWindow wnd!)
        #super!.create(wnd!)
    methodend

    rem /**
    rem  * This method is called whenever the widget needs to be rendered.
    rem  *
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  * @Override
    rem  */
    method public void redraw(Boolean f_init!)
        if (f_init!) then
            rem rendering for the first time
            rem so have to initialize / create my controls
            #grid! = new BBjGridExWidget(#getCanvas(),100,0,30,#getWidth(),#getHeight()-30)
            #grid!.getOptions().setNavigationBehavior(#grid!.GRID_NAVIGATION_BEHAVIOUR_NEXT_ROW())
            #grid!.setCallback(#grid!.ON_GRID_ROW_SELECT(),#this!,"onGridSelection")
            #cfgbc! = new CustomConfigurationsBC(info(3,2))

            options! = #grid!.getOptions()
            menu! = new GxContextMenu()
            menu!.addItem(GxContextMenuItem.AUTO_SIZE_ALL())
            menu!.addItem(GxContextMenuItem.RESET_COLUMNS())
            options!.setContextMenu(menu!)

            #exportButton! = new ExportMenuButton(#getCanvas(), 202, 0, 5, 43, 20)
            #exportButton!.setGrid(#this!)
            #exportButton!.setBC(new CustomConfigurationsBC(info(3,2), "exportButtonConfigurations"))
            #exportButton!.setProgramName(pgm(-2,0))
            
            #fitToGridButton! = #getCanvas().addMenuButton(203, 48, 5, 20, 20, "")
            #fitToGridButton!.setBorderPainted(Boolean.TRUE)
            #fitToGridButton!.setImageSize(23, 23)
            #fitToGridButton!.setCallback(#fitToGridButton!.ON_BUTTON_PUSH, #this!, "fitToGrid")
            
            #updateProperties()
        fi

        #grid!.setSize(#getWidth(),#getHeight()-30)
        if #cfgwidget! <> null() then
            rem min padding for config widget because of the print button to the left of it
            #cfgwidget!.setMinPaddingLeft(50)
            #cfgwidget!.setLocation(#getWidth()-155, 5)
        fi
    methodend
    
    method protected void updateProperties()
        #super!.updateProperties()
        uiProperties! = #getUIPropertiesManager()
        #exportButton!.setImageFile(uiProperties!.getProperty("EXPORT_BTN_IMAGE"))
        #fitToGridButton!.setImageFile(uiProperties!.getProperty("FIT_TO_GRID_IMAGE"))
        
        height = uiProperties!.getPropertyAsNumber("CONFIG_WIDGET_HEIGHT")
        if height > 0 then 
            #exportButton!.setSize(height*2, height)
            #exportButton!.setImageSize(height-4, height-4)
            #fitToGridButton!.setSize(height, height)
            #fitToGridButton!.setImageSize(height-4, height-4)
        endif
    methodend

    rem ---------------------- Event Handlers --------------------------------

    rem -----------------------getter and setter -----------------------------
    rem /**
    rem  *
    rem  */
    method public BBjGridExWidget getGrid()
        methodret #grid!
    methodend

    method public void setConfigurationWidgetUser(String user!)
        #cfgbc!.setUserName(user!)
    methodend

    rem ----------------------grid handling---------------------------------------------

    rem /**
    rem  *
    rem  */
    method public void onSetData()
        declare ResultSet rs!
        declare ResultSet rsa!
        declare DataRow ar!
        declare HashMap fa!
        declare GxColumn column!
        declare GxRendererCustomHTML blankWhenZeroRenderer!
        declare GxRendererBoolean checkboxRenderer!

        rs! = #getBinder().getRS()
        ar! = #getBinder().getAttributesRecord()
        if rs! = null() or ar! = null() then methodret

        if rs!.getColumnCount() and !(ar!.isEmpty()) then

            if !(#columnConfigurationSet!) then

                rem render grid columns based on the attributes record (w/o any data)
                rsa! = new ResultSet()
                rsa!.add(ar!)
                #grid!.setData(rsa!)

                for rscol = 0 to rs!.getColumnCount() - 1

                    type = rs!.getColumnType(rscol)
                    fieldName$ = rs!.getColumnName(rscol)
                    column! = #grid!.getColumn(fieldName$)

                    fa! = ar!.getFieldAttributes(fieldName$);rem returns HashMap

                    label$ = fieldName$
                    if fa!.containsKey("LABEL") then label$ = fa!.get("LABEL")
                    column!.setLabel(label$)

                    mask$ = ""
                    if fa!.containsKey("MASK") then mask$ = cvs(fa!.get("MASK"),3)
                    if mask$ <> "" then column!.setColumnMask(mask$)

                    if type = java.sql.Types.DATE or type = java.sql.Types.TIME or type = java.sql.Types.BOOLEAN then
                        column!.setHeaderAlignment(#grid!.GRID_ALIGN_CENTER())
                        column!.setAlignment(#grid!.GRID_ALIGN_CENTER())
                    else
                        if SqlTypeNames.isNumericType(type) then
                            column!.setHeaderAlignment(#grid!.GRID_ALIGN_RIGHT())
                            column!.setAlignment(#grid!.GRID_ALIGN_RIGHT())
                            if mask$ = "" then
                                rem blank when zero
                                blankWhenZeroRenderer! = new GxRendererCustomHTML("<% print(params.data."+fieldName$+" == 0 ? '&nbsp;' : params.data."+fieldName$+") %>")
                                column!.setCellRenderer(blankWhenZeroRenderer!)
                            endif
                        endif
                    endif

                    if type = java.sql.Types.BOOLEAN then
                        checkboxRenderer! = new GxRendererBoolean()
                        checkboxRenderer!.setTrueValue("<input type='checkbox' checked/>")
                        checkboxRenderer!.setFalseValue("<input type='checkbox' />")
                        column!.setCellRenderer(checkboxRenderer!)
                        rem column!.getCellRenderer().setTrueValue(GxRendererBoolean.SWITCH_RENDERER())
                        rem column!.getCellRenderer().setFalseValue(GxRendererBoolean.SWITCH_RENDERER())
                    endif

                    if fa!.containsKey("PASSWORD") then
                        column!.setCellRenderer(new GxRendererCustomHTML("<%= ""<input readonly type='password' value='"" + params.value + ""' />"" %>"))
                    endif

                    hide = BBjAPI.FALSE
                    if fa!.containsKey("HIDE") then hide = num(fa!.get("HIDE"))
                    if hide then column!.setHidden(hide)

                next rscol

                #columnConfigurationSet! = Boolean.TRUE
            endif

        endif
        
        #grid!.updateData(rs!)
    methodend

    rem /**
    rem  *
    rem  */
    method public void onSetSelection()
            if (#sending!) then
                methodret
            fi

            sel! = #getBinder().getSelection()
            rs! = #getBinder().getRS()

            it! = sel!.iterator()
            s! = new BBjVector()
            while it!.hasNext()
                rowkey$ = it!.next()
                i = rs!.indexOf(rowkey$)
                s!.addItem(i)
            wend

            #grid!.deselectAll()
            if (s!.size()>0) then
                #grid!.setSelectedRows(s!)
                #grid!.ensureIndexVisible(s!.getItem(0),#grid!.GRID_ROWPOS_MIDDLE())
            fi
    methodend

    rem /**
    rem  * @param ev! BBj custom event for row select
    rem  */
    method public void onGridSelection(BBjCustomEvent ev!)
        if #ignoreNext! then
            #ignoreNext! = BBjAPI.FALSE
            methodret
        fi

        if #getBinder().canSetSelection() then
              x! = ev!.getObject()
              rows! = x!.getSelectedRows()
              sel! = new BBjVector()
              it! = rows!.iterator()
              while it!.hasNext()
                  row! = it!.next()
                  sel!.addItem(row!.asDataRow().getRowKey())
              wend
              #sending! = BBjAPI.TRUE
              #getBinder().setSelection(sel!)
              #sending! = BBjAPI.FALSE
         else
            #onSetSelection()
            #ignoreNext! = BBjAPI.TRUE
         fi
    methodend

    rem methods from interface IPersistentState

    rem /**
    rem  * This method will be called to apply the state to the widget.
    rem  *
    rem  * @param state! state string previously stored by the widget
    rem  */
    method public void applyState(String state!)
        #grid!.setColumnState(new GxState(state!))
    methodend

    rem /**
    rem  * This method will provide the widget with the current state of the component.<br>
    rem  * The widget will store this data for later retrieval.
    rem  */
    method public String retrieveState()
        methodret #grid!.getColumnState().toString()
    methodend

    rem /**
    rem  * The widget will call this method to register itself for state changes of the component.<br>
    rem  * The method should set a callback that calls the method 'methodName' on 'callable!', whenever the state changes.<br>
    rem  * Event should be a BBjCustomEvent.
    rem  *
    rem  * @param methodName!
    rem  * @param callable!
    rem  */
    method public void setStateChangeCallback(String methodName!, CustomObject callable!)
       #grid!.setCallback(#grid!.ON_GRID_COLUMN_STATE_CHANGE(),callable!,methodName!)
    methodend

    rem /**
    rem  * Registers the governing BCBinder and creates the configuration widget.
    rem  *
    rem  * @param binder!
    rem  */
    method public void setBinder(BCBinder binder!)
        #super!.setBinder(binder!)
        x$=str(binder!.getBC())
        while pos("::"=x$)>0
            x$=X$(pos("::"=x$)+2)
        wend
        #cfgwidget! = new ConfigurationWidget(#getCanvas(),201, #getWidth()-155, 5 , #this!, Boolean.TRUE, "GRD", x$, #cfgbc! )
    methodend
    
    method public void fitToGrid(BBjEvent ev!)
        #fitToGrid()
    methodend
    
    method private void fitToGrid()
        if #grid!<>null() then
            #grid!.setFitToGrid()
        fi
    methodend

classend
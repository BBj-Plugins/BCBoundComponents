use ::BusinessUIComponents/AbstractBCBoundWidget.bbj::AbstractBCBoundWidget
use ::BusinessUIComponents/ExportMenuButton.bbj::ExportMenuButton
use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget

use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow
use com.basiscomponents.bc.BCBinder

use ::BBjGridExWidget/GxClientModels.bbj::GxClientRowModel
use ::BBjGridExWidget/GxState.bbj::GxState

use ::BusinessUIComponents/configurations/ConfigurationWidget.bbj::ConfigurationWidget
use ::BusinessUIComponents/configurations/CustomConfigurationsBC.bbj::CustomConfigurationsBC
use ::BusinessUIComponents/configurations/IPersistentState.bbj::IPersistentState


rem /**
rem  * BBj Business Grid<br>
rem  * Uses a Business Component (BC), BCBinder and BBjGridExWidget.
rem  */
class public BusinessGrid extends AbstractBCBoundWidget implements IPersistentState

    field private BBjGridExWidget grid!
    field private Boolean sending! = BBjAPI.FALSE
    field private Boolean ignoreNext! = BBjAPI.FALSE

    field private ConfigurationWidget    cfgwidget!
    field private CustomConfigurationsBC cfgbc!
    field private ExportMenuButton       exportButton!
    
    field private Boolean columnConfigurationSet! = Boolean.FALSE

    rem --------------------- constructors -------------------------------------

    rem /**
    rem  * disabled default constructor
    rem  */
    method private BusinessGrid()
    methodend

    rem /**
    rem  * The constructor that creates the child-window (canvas) on which the widget is created.
    rem  *
    rem  * @param BBjWindow wnd!: parent window
    rem  * @param BBjInt id: the control ID
    rem  * @param BBjInt x: x-location
    rem  * @param BBjInt y: y-location
    rem  * @param BBjInt w: width
    rem  * @param BBjInt h: height
    rem  */
    method public BusinessGrid(BBjWindow wnd!, BBjInt id!, BBjInt x!, BBjInt y!, BBjInt w!, BBjInt h!)
        #super!.create(wnd!,id!,x!,y!,w!,h!)
    methodend

    rem /**
    rem  * The constructor that an existing child-window (canvas) on which the widget is created.
    rem  */
    method public BusinessGrid(BBjChildWindow wnd!)
        #super!.create(wnd!)
    methodend

    rem /**
    rem  * This method is called whenever the widget needs to be rendered.
    rem  *
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  * @Override
    rem  */
    method public void redraw(Boolean f_init!)
        if (f_init!) then
            rem rendering for the first time
            rem so have to initialize / create my controls
            #grid! = new BBjGridExWidget(#getCanvas(),100,0,30,#getWidth(),#getHeight()-30)
            #grid!.getOptions().setNavigationBehavior(#grid!.GRID_NAVIGATION_BEHAVIOUR_NEXT_ROW())
            #grid!.setCallback(#grid!.ON_GRID_ROW_SELECT(),#this!,"onGridSelection")
            #cfgbc! = new CustomConfigurationsBC(info(3,2))

            rem TODO: exportbutton
            #exportButton! = new ExportMenuButton(#getCanvas(), 202, 0, 5, 43, 23)
            #exportButton!.setGrid(#this!)
            #exportButton!.setBC(new CustomConfigurationsBC(info(3,2), "exportButtonConfigurations"))
            #exportButton!.setProgramName(pgm(-2,0))
        fi

        #grid!.setSize(#getWidth(),#getHeight()-30)
        if #cfgwidget! <> null() then
            rem min padding for config widget because of the print button to the left of it
            #cfgwidget!.setMinPaddingLeft(50)
            #cfgwidget!.setLocation(#getWidth()-155, 5)
        fi
    methodend

    rem ---------------------- Event Handlers --------------------------------

    rem -----------------------getter and setter -----------------------------
    rem /**
    rem  *
    rem  */
    method public BBjGridExWidget getGrid()
        methodret #grid!
    methodend


    rem ----------------------grid handling---------------------------------------------

    rem /**
    rem  *
    rem  */
    method public void onSetData()
        if #getBinder().getRS() = null() or #getBinder().getAttributesRecord() = null() then
            methodret
        fi
        
        if !#columnConfigurationSet! then
            attrRecord! = #getBinder().getAttributesRecord()
            rs! = new ResultSet()
            rs!.add(attrRecord!)
            #grid!.setData(rs!)
            
            if !attrRecord!.isEmpty() then 
                fieldNames! = attrRecord!.getFieldNames()
                
                for i = 0 to fieldNames!.size() - 1
                    fieldName$ = fieldNames!.get(i)
                    
                    attributes! = attrRecord!.getFieldAttributes(fieldName$)
                    header$ = fieldName$
                    if attributes!.containsKey("LABEL") then
                        header$ = attributes!.get("LABEL")
                    endif
                    
                    #grid!.addColumn(fieldName$, header$)
                    
                    if attributes!.containsKey("ColumnTypeName") then
                        columnTypeName$ = attributes!.get("ColumnTypeName")
                        
                        switch columnTypeName$
                            case "NUMERIC"
                                #grid!.setColumnAlignment(fieldName$, #grid!.GRID_ALIGN_RIGHT())
                            break
                            
                            case "BOOLEAN"
                                column! = #grid!.getColumn(fieldName$)
                                column!.getCellRenderer().setTrueValue(GxRendererBoolean.SWITCH_RENDERER())
                                column!.getCellRenderer().setFalseValue(GxRendererBoolean.SWITCH_RENDERER())
                            case "DATE"
                                #grid!.setColumnAlignment(fieldName$, #grid!.GRID_ALIGN_CENTER())
                            break
                        swend
                        
                    endif
                    
                    if attributes!.containsKey("MASK") then
                        #grid!.setColumnMask(fieldName$, attributes!.get("MASK"))
                    endif
                    
                next i
            endif
            #columnConfigurationSet! = Boolean.TRUE
        endif
        
        #grid!.setData(#getBinder().getRS())
    methodend

    rem /**
    rem  *
    rem  */
    method public void onSetSelection()
            if (#sending!) then
                methodret
            fi

            sel! = #getBinder().getSelection()
            rs! = #getBinder().getRS()

            it! = sel!.iterator()
            s! = new BBjVector()
            while it!.hasNext()
                rowkey$ = it!.next()
                i = rs!.indexOf(rowkey$)
                s!.addItem(i)
            wend

            #grid!.deselectAll()
            if (s!.size()>0) then
                #grid!.setSelectedRows(s!)
                #grid!.ensureIndexVisible(s!.getItem(0),#grid!.GRID_ROWPOS_MIDDLE())
            fi
    methodend

    rem /**
    rem  * @param ev! BBj custom event for row select
    rem  */
    method public void onGridSelection(BBjCustomEvent ev!)
        if #ignoreNext! then
            #ignoreNext! = BBjAPI.FALSE
            methodret
        fi

        if #getBinder().canSetSelection() then
              x! = ev!.getObject()
              rows! = x!.getSelectedRows()
              sel! = new BBjVector()
              it! = rows!.iterator()
              while it!.hasNext()
                  row! = it!.next()
                  sel!.addItem(row!.asDataRow().getRowKey())
              wend
              #sending! = BBjAPI.TRUE
              #getBinder().setSelection(sel!)
              #sending! = BBjAPI.FALSE
         else
            #onSetSelection()
            #ignoreNext! = BBjAPI.TRUE
         fi
    methodend

    rem methods from interface IPersistentState

    rem /**
    rem  * This method will be called to apply the state to the widget.
    rem  *
    rem  * @param state! state string previously stored by the widget
    rem  */
    method public void applyState(String state!)
        #grid!.setColumnState(new GxState(state!))
    methodend

    rem /**
    rem  * This method will provide the widget with the current state of the component.<br>
    rem  * The widget will store this data for later retrieval.
    rem  */
    method public String retrieveState()
        methodret #grid!.getColumnState().toString()
    methodend

    rem /**
    rem  * The widget will call this method to register itself for state changes of the component.<br>
    rem  * The method should set a callback that calls the method 'methodName' on 'callable!', whenever the state changes.<br>
    rem  * Event should be a BBjCustomEvent.
    rem  *
    rem  * @param methodName!
    rem  * @param callable!
    rem  */
    method public void setStateChangeCallback(String methodName!, CustomObject callable!)
       #grid!.setCallback(#grid!.ON_GRID_COLUMN_STATE_CHANGE(),callable!,methodName!)
    methodend

    rem /**
    rem  * Registers the governing BCBinder and creates the configuration widget.
    rem  *
    rem  * @param binder!
    rem  */
    method public void setBinder(BCBinder binder!)
        #super!.setBinder(binder!)
        x$=str(binder!.getBC())
        while pos("::"=x$)>0
            x$=X$(pos("::"=x$)+2)
        wend
        #cfgwidget! = new ConfigurationWidget(#getCanvas(),201, #getWidth()-155, 5 , #this!, Boolean.TRUE, "GRD", x$, #cfgbc! )
    methodend

classend
use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::BusinessUIComponents/AbstractBCBoundWidget.bbj::AbstractBCBoundWidget
use ::BusinessUIComponents/BusinessGrid.bbj::BusinessGrid
use ::BusinessUIComponents/bound/TestComponent.bbj::TestComponent
use ::BusinessUIComponents/bound/NavigatorComponent.bbj::NavigatorComponent
use ::BusinessUIComponents/bound/EditToolbarComponent.bbj::EditToolbarComponent
use ::BusinessUIComponents/bound/SimpleFormComponent.bbj::SimpleFormComponent
use ::BusinessUIComponents/bound/FilterComponent.bbj::FilterComponent
use ::BusinessUIComponents/bound/SearchBar.bbj::SearchBar
use ::BusinessUIComponents/bound/MenuBar.bbj::MenuBar
use ::BusinessUIComponents/bound/BCErrorHandler.bbj::BCErrorHandler
use ::BusinessUIComponents/bound/WindowHandler.bbj::WindowHandler
use ::BusinessUIComponents/unbound/HorizontalSplitter.bbj::HorizontalSplitter
use ::BusinessUIComponents/configurations/IPropertiesManager.bbj::IPropertiesManager
use ::BusinessUIComponents/configurations/LocalizedPropertiesManager.bbj::LocalizedPropertiesManager
use com.basiscomponents.bc.BCBinder
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.ResultSet
use java.util.HashMap

class public BusinessMonitorUI

    field protected BBjTopLevelWindow wnd!
    field protected WindowHandler wh!
    field protected EditToolbarComponent edittb!
    field protected HorizontalSplitter splitter!
    field protected BBjVector filters!
    field protected BBjTabCtrl filterTabCtrl!
    field protected BCBinder binder!
    field protected BBjSysGui sysgui!
    field protected BBjVector componentsWithProperties! = new BBjVector()
    
    
    field protected String title! = ""
    field protected String pgm! = ""
    

    method public BusinessMonitorUI( BBjString BC$, BBjString TITLE$, BBjString PGMNAME$)
        ? 'HIDE'
        #sysgui! = BBjAPI().openSysGui("X0")
        #sysgui!.setRepaintEnabled(0)
        
        if info(3,6)="5" then
            #wnd! = #sysgui!.addWindow(100,100,1200,760,TITLE$,$01001010$)
        else
            #wnd! = #sysgui!.addWindow(100,100,1200,760,TITLE$,$00000093$)
        fi
        
        #wnd!.setCallback(BBjAPI.ON_RESIZE, #this!,"resize")
        
        #splitter! = new HorizontalSplitter(#wnd!,112,2,30,890,800)
        
        #splitter!.setBottomCtrl(bgrid!)
        
        
        #filters! = new BBjVector()
        
        bc! = eval("new "+BC$+"()")
        
        #binder! = new BCBinder(bc!)
        #binder!.setDebugLevel(1)
        #title! = TITLE$
        #pgm! = PGMNAME$
    methodend
    
    method public void doModal()
        #binder!.retrieve()
        
        #delayed_resize(null())
        
        #sysgui!.setRepaintEnabled(1)
        #wnd!.setVisible(1)
        
        process_events
    methodend
    
    rem ######### propertiesManager Setters #########
    
    method public void setTextPropertiesManager(IPropertiesManager pm!)
        it! = #componentsWithProperties!.iterator()
        while it!.hasNext()
            component! = it!.next()
            cast(AbstractBCBoundWidget, component!).setTextPropertiesManager(pm!)
        wend
    methodend
    
    method public void setUIPropertiesManager(IPropertiesManager pm!)
        it! = #componentsWithProperties!.iterator()
        while it!.hasNext()
            component! = it!.next()
            cast(AbstractBCBoundWidget, component!).setUIPropertiesManager(pm!)
        wend
        if #wh! <> null() then #wh!.setUIPropertiesManager(pm!)
    methodend
    
    rem ######### add methods for BC bound components #########
    
    method public void addFilters(ResultSet filter_defs!, HashMap controlPrefixMap!)
        cwIdStart = 200
        
        if filter_defs!.size() = 1 then
            dr! = filter_defs!.get(0)
            filterCW! = #splitter!.getTopCanvas()
            if (dr!.contains("CLASSNAME")) then
                filter! = eval("new "+dr!.getFieldAsString("CLASSNAME")+"(filterCW!,100,0,5,300,300)")
            else
                filter! = new FilterComponent(#binder!, filterCW!,100,2,10,300,300)
                filter!.setKey(#pgm!)
                filter!.setForm(dr!.getFieldAsString("ARC"), dr!.getFieldAsNumber("CWID"))
                #componentsWithProperties!.add(filter!)
            fi
            if controlPrefixMap! <> null() then
                filter!.setControlPrefixMap(controlPrefixMap!)
            endif
            #filters!.addItem(filter!)
REM             #binder!.register(filter!)
            filter!.buildControlMap()
            
            methodret
        endif
        #filterTabCtrl! = #splitter!.getTopCanvas().addTabCtrl(115, 0, 10, 500, 500)
        #splitter!.setTopCtrl(#filterTabCtrl!)
        it! = filter_defs!.iterator()
        while it!.hasNext()
            dr! = it!.next()
            filterCW! = #splitter!.getTopCanvas().addChildWindow(cwIdStart + i,0,0,500,500,"",$00000800$, #sysgui!.getAvailableContext(),$$)
            #filterTabCtrl!.addTab(dr!.getFieldAsString("TABNAME"), filterCW!)
            if (dr!.contains("CLASSNAME")) then
                filter! = eval("new "+dr!.getFieldAsString("CLASSNAME")+"(filterCW!,100,0,5,300,300)")
            else
                filter! = new FilterComponent(#binder!, filterCW!,100,0,10,300,300)
                filter!.setKey(#pgm!)
                filter!.setForm(dr!.getFieldAsString("ARC"), dr!.getFieldAsNumber("CWID"))
                #componentsWithProperties!.add(filter!)
            fi
            if controlPrefixMap! <> null() then
                filter!.setControlPrefixMap(controlPrefixMap!)
            endif
            #filters!.addItem(filter!)
REM             #binder!.register(filter!)
            filter!.buildControlMap()
            cwIdStart = cwIdStart + 1
        wend
    methodend
    
    method public BCBinder getBinder()
        methodret #binder!
    methodend

    method public void addMenuBar()
        menuBar! = new MenuBar(#wnd!)
        #binder!.register(menuBar!)
        menuBar!.setBinder(#binder!)
        REM #componentsWithProperties!.add(menuBar!) ; rem currently useless
        menuBar!.setTextPropertiesManager(LocalizedPropertiesManager.getInstance())
        menuBar!.initMonitor()
    methodend
    
    method public void addEditToolbar(BBjInt x!, BBjInt y!)
        #edittb! = new EditToolbarComponent(#wnd!,111,x!,y!,100,32)
        #edittb!.setEnabledButtons(0, 0, 0, 0, 1)
        #binder!.register(#edittb!)
        #componentsWithProperties!.add(#edittb!)
    methodend
    
    method public void addBusinessGrid(BBjInt x!, BBjInt y!)
        bgrid! = new BusinessGrid(#splitter!.getBottomCanvas(),160,x!,y!,960,500)
        #splitter!.setBottomCtrl(bgrid!)
        #binder!.register(bgrid!)
        #componentsWithProperties!.add(bgrid!)
    methodend
    method public void addBCErrorHandler(String title!)
        errorhandler! = new BCErrorHandler()
        errorhandler!.setTitle(title!)
        #binder!.register(errorhandler!)
    methodend
    
    method public void addWindowHandler(String pgmName!)
        #wh! = new WindowHandler(#wnd!)
        #wh!.setProgramName(pgmName!)
        #wh!.applyWindowConfig()
        #binder!.register(#wh!)
    methodend
    
    rem ############## Events ######################
    
    method public void resize(BBjEvent ev!)
        BBjAPI().removeTimer("resize",err=*next)
        BBjAPI().createTimer("resize", .4, #this!, "delayed_resize")
    methodend
    
    method public void delayed_resize(BBjEvent ev!)
        BBjAPI().removeTimer("resize",err=*next)
        w = #wnd!.getWidth()
        h = #wnd!.getHeight()
        #splitter!.setSize(w-4 ,h-25)
        for i = 0 to #filters!.size() - 1
          cast(BBjWidget, #filters!.getItem(i)).setSize(w-4, h-25)
        next i
        if #edittb! <> null() then
            #edittb!.redraw(0)
        endif
    methodend
    
    

    method public BBjVector getFilters()
        methodret #filters!
    methodend

    method public BBjWindow getWindow()
        methodret #wnd!
    methodend

classend

declare ResultSet filter_defs!

rem controlPrefixMap! is optional
controlPrefixMap! = null()
enter filter_defs!, BC$, TITLE$, PGMNAME$, controlPrefixMap!, err=*next

pgm! = PGMNAME$
if pgm!.length() > 40 then
    pgm! = pgm!.substring(pgm!.length() -35)
endif
PGMNAME$ = pgm!
md! = new BusinessMonitorUI(BC$, TITLE$, PGMNAME$)

md!.addMenuBar()
md!.addEditToolbar(2,2)
md!.addBusinessGrid(0,5)
md!.addBCErrorHandler(TITLE$)
md!.addWindowHandler(TITLE$)
md!.addFilters(filter_defs!, controlPrefixMap!)

md!.doModal()
